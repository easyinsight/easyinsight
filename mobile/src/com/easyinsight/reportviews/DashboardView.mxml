<?xml version="1.0"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark"
    xmlns:mx="library://ns.adobe.com/flex/mx" viewActivate="onActivate()">
    <fx:Script><![CDATA[
        import com.easyinsight.baseviews.MobileLoginComponent;
        import com.easyinsight.dashboard.Dashboard;
        import com.easyinsight.dashboard.DashboardDescriptor;
        import com.easyinsight.dashboard.DashboardEditorMetadata;
        import com.easyinsight.dashboard.IDashboardViewComponent;
        import com.easyinsight.framework.User;

        import mx.core.UIComponent;

        import mx.events.FlexEvent;
        import mx.messaging.ChannelSet;
        import mx.messaging.config.ServerConfig;
        import mx.rpc.AsyncResponder;
        import mx.rpc.AsyncToken;
        import mx.rpc.events.FaultEvent;
        import mx.rpc.events.ResultEvent;

        private var dashboardDescriptor:DashboardDescriptor;

        private function onActivate():void {
            removeEventListener(LogoutEvent.LOGOUT, onLogout);
            addEventListener(LogoutEvent.LOGOUT, onLogout);
            dashboardDescriptor = data as DashboardDescriptor;
            dashboardName = dashboardDescriptor.name;
            dashboardService.getDashboard.send(dashboardDescriptor.id);
        }

        private function onLogout(event:LogoutEvent):void {
            User.destroy();
            var cs:ChannelSet = ServerConfig.getChannelSet("admin");
            var token:AsyncToken = cs.logout();
            token.addResponder(new AsyncResponder(logoutResultEvent, logoutFaultEvent));
        }

        private function logoutResultEvent(event:ResultEvent, token:Object = null):void {
            navigator.replaceView(MobileLoginComponent);
        }

        private function logoutFaultEvent(event:FaultEvent, token:Object = null):void {
            trace("Failed to log out with " + event.fault.faultString);
        }

        private var rootView:IDashboardViewComponent;

        private function gotDashboard():void {
            var dashboard:Dashboard = dashboardService.getDashboard.lastResult as Dashboard;
            var metadata:DashboardEditorMetadata = new DashboardEditorMetadata();
            metadata.dataSourceID = dashboard.dataSourceID;
            metadata.dashboardView = this;
            metadata.role = dashboard.role;
            rootView = DashboardMobileFactory.createViewUIComponent(dashboard.rootElement, metadata);
            UIComponent(rootView).addEventListener(FlexEvent.CREATION_COMPLETE, function(event:FlexEvent):void {
                var filterMap:Object = new Object();
                filterMap["master"] = dashboard.filters;
                rootView.updateAdditionalFilters(filterMap);
                rootView.initialRetrieve();
            });
            addElement(rootView as UIComponent);
        }

        [Bindable]
        private var busy:Boolean = false;

        [Bindable]
        private var dashboardName:String;
        ]]></fx:Script>
    <fx:Declarations>
        <mx:RemoteObject id="dashboardService" destination="dashboardService" endpoint="https://www.easy-insight.com/app/messagebroker/amfsecure">
            <mx:method name="getDashboard" result="gotDashboard()"/>
        </mx:RemoteObject>
    </fx:Declarations>
    <s:navigationContent>
        <s:Button label="Back" click="navigator.popView()"/>
    </s:navigationContent>
    <s:titleContent>
        <s:Label text="{dashboardName}" color="#FFFFFF"/>
    </s:titleContent>
    <s:actionContent>
        <s:BusyIndicator visible="{busy}" id="busyIndicator" symbolColor="#FFFFFF"/>
    </s:actionContent>
    <!--<s:Scroller id="scroller">
        <s:Group id="group" width="1000" height="600"/>
    </s:Scroller>-->
</s:View>
