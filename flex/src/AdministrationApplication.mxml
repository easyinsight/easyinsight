<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:account="com.easyinsight.account.*"
                xmlns:solutions="com.easyinsight.solutions.*" layout="absolute"
                creationComplete="setup()"
                xmlns:system="com.easyinsight.administration.system.*">
    <mx:states>
        <mx:State name="loggedIn">
            <mx:RemoveChild target="{loginForm}"/>
            <mx:AddChild relativeTo="{coreBox}">
                <mx:TabNavigator width="100%" height="100%">
                    <mx:VBox width="100%" height="100%" label="System Health">
                        <system:HealthInfoDisplay/>
                        <mx:Button label="Thread Dump" click="dumpThreads()"/>
                        <mx:Button label="Update Zip Geocodes" click="updateZipCodes()"/>
                        <mx:Button label="Nuke" click="nuke()"/>
                        <mx:Button label="Apply Days" click="applyDays()"/>
                        <mx:Button label="Report Admin XML" click="reportAdminXML()"/>
                        <mx:Button label="Report Test" click="reportTest()"/>
                        <mx:Button label="Global Skin" click="globalSkin()"/>
                        <mx:Button label="Header Logo" click="headerLogo()"/>
                        <mx:Button label="Synchronize" click="sync()"/>
                        <mx:Button label="Archive" click="archive()"/>
                    </mx:VBox>
                    <mx:VBox width="100%" height="100%" label="Connections">
                        <solutions:SolutionAdministration width="100%" height="100%"/>
                    </mx:VBox>
                    <mx:VBox width="100%" height="100%" label="Accounts">
                        <account:AccountAdministration width="100%" height="100%"/>
                    </mx:VBox>
                    <account:Newsletter label="Newsletter"/>
                    <account:NewsUpdate label="News Update"/>
                </mx:TabNavigator>
            </mx:AddChild>
        </mx:State>
    </mx:states>
    <mx:Script>
		<![CDATA[
        import com.easyinsight.account.Account;
        import com.easyinsight.administration.ArchiveWindow;
        import com.easyinsight.administration.ReportAdminXMLWindow;
        import com.easyinsight.administration.ReportTestWindow;
        import com.easyinsight.administration.UpdateZipsWindow;
        import com.easyinsight.framework.LoginEvent;
        import com.easyinsight.framework.LoginObject;
        import com.easyinsight.framework.LoginResponse;
        import com.easyinsight.framework.User;
        import com.easyinsight.framework.UserServiceResponse;
        import com.easyinsight.skin.GlobalSkinWindow;
        import com.easyinsight.skin.HeaderImageCreator;
        import com.easyinsight.util.PopUpUtil;
        import com.easyinsight.util.ProgressAlert;

        import mx.managers.PopUpManager;
        import mx.messaging.config.ServerConfig;
        import mx.rpc.AsyncResponder;
        import mx.rpc.AsyncToken;
        import mx.rpc.events.FaultEvent;
        import mx.rpc.events.ResultEvent;

        private function headerLogo():void {
            var window:HeaderImageCreator = new HeaderImageCreator();
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }
        
        private function sync():void {
            adminService.constantContactSync.send();
        }

        private function updateZipCodes():void {
            var window:UpdateZipsWindow = new UpdateZipsWindow();
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function reportAdminXML():void {
            var window:ReportAdminXMLWindow = new ReportAdminXMLWindow();
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function archive():void {
            var window:ArchiveWindow = new ArchiveWindow();
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function nuke():void {
            adminService.delinquentToExpired.send();
        }

        private function applyDays():void {
            adminService.applyDays.send();
        }

        private function reportTest():void {
            var window:ReportTestWindow = new ReportTestWindow();
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function handleAuthentication():void {
            var authResult:UserServiceResponse = authAdminService.authenticateAdmin.lastResult as UserServiceResponse;
            var successful:Boolean = authResult.successful;
            if (successful) {
                User.initializeUser(authResult);
                User.getInstance().userName = authResult.userName;
                currentState = "loggedIn";
            } else {
                failureMessageLabel.text = authResult.failureMessage;
            }
        }

        private function login():void {
            if (authService.channelSet == null) {
                authService.channelSet = ServerConfig.getChannelSet(authService.destination);
            }
            var token:AsyncToken = authService.channelSet.login(userName.text, password.text);
            token.addResponder(new AsyncResponder(
                    function (event:ResultEvent, token:Object = null):void {
                        switch (event.result) {
                            case "success":
                                authAdminService.authenticateAdmin.send(userName.text, password.text);
                                break;
                            default:
                                trace(event.result);
                        }
                    },
                    function (event:FaultEvent, token:Object = null):void {
                        switch (event.fault.faultCode) {
                            case "Client.Authentication":
                            default:
                                var failureMessage:String = event.fault.faultString;
                                failureMessageLabel.text = failureMessage;
                        }
                    }
                    ));
        }

        private function setup():void {
            authService.isSessionLoggedIn.send();
            //var user:User = User.getInstance();
            //if (user == null) {
            //	currentState = 'notLoggedIn';
            //}
        }

        private function dumpThreads():void {
            adminService.threadDump.send();
        }


        private function onLogin(event:LoginEvent):void {
            var authResult:UserServiceResponse = event.authResponse;
            if (authResult.accountType == Account.ADMINISTRATOR) {
                User.initializeUser(authResult);
                User.getInstance().userName = authResult.userName;
                currentState = "loggedIn";
            }
        }

        private var loginObject:LoginObject;

        private function sessionCheck():void {
            var loginResponse:LoginResponse = authService.isSessionLoggedIn.lastResult as LoginResponse;
            if (loginResponse != null && loginResponse.token != null) {
                loginObject = new LoginObject();
                loginObject.addEventListener(LoginEvent.LOGIN, onLogin);
                loginObject.authenticate(loginResponse.token, loginResponse.userIDString, loginResponse.userServiceResponse);
            } else if (loginResponse != null) {
                var authResult:UserServiceResponse = loginResponse.userServiceResponse;
                if (authResult.accountType == Account.ADMINISTRATOR) {
                    User.initializeUser(authResult);
                    User.getInstance().userName = authResult.userName;
                    currentState = "loggedIn";
                }
            }
        }

        private function testSelenium():void {
            ProgressAlert.alert(this, "Testing Selenium...", null, exportService.test);
            exportService.test.send();
        }

        private function nukeOldData():void {
            ProgressAlert.alert(this, "Clearing orphan data...", null, adminService.clearOrphanData);
            adminService.clearOrphanData.send();
        }

        private function specialOffer():void {
            ProgressAlert.alert(this, "Updating...", null, authAdminService.specialOffer);
            authAdminService.specialOffer.send();
        }

        private function globalSkin():void {
            var window:GlobalSkinWindow = new GlobalSkinWindow();
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function offered():void {

        }
		]]>
    </mx:Script>
    <mx:RemoteObject id="authService" destination="login">
        <mx:method name="isSessionLoggedIn" result="sessionCheck()"/>
    </mx:RemoteObject>
    <mx:RemoteObject id="exportService" destination="exportService">
        <mx:method name="test"/>
    </mx:RemoteObject>
    <mx:RemoteObject id="authAdminService" destination="eiAdmin">
        <mx:method name="authenticateAdmin" result="handleAuthentication()"/>
        <mx:method name="specialOffer" result="offered()"/>
    </mx:RemoteObject>
    <mx:RemoteObject id="adminService" destination="admin">
        <mx:method name="threadDump"/>
        <mx:method name="applyDays"/>
        <mx:method name="constantContactSync"/>
        <mx:method name="clearOrphanData"/>
        <mx:method name="delinquentToExpired"/>
    </mx:RemoteObject>
    <mx:Style source="com/easyinsight/skin/osx/OSX.css"/>
    <mx:VBox id="coreBox" width="100%" height="100%" horizontalAlign="center">
        <mx:Form id="loginForm">
            <mx:FormItem label="User Name: ">
                <mx:TextInput id="userName"/>
            </mx:FormItem>
            <mx:FormItem label="Password: ">
                <mx:TextInput id="password" displayAsPassword="true"/>
            </mx:FormItem>
            <mx:FormItem label="">
                <mx:Label id="failureMessageLabel"/>
            </mx:FormItem>
            <mx:FormItem label="">
                <mx:Button label="Submit" click="login()"/>
            </mx:FormItem>
        </mx:Form>
    </mx:VBox>

</mx:Application>
