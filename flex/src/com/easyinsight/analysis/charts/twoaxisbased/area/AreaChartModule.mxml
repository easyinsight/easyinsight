<?xml version="1.0" ?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" implements="com.easyinsight.analysis.IReportRenderer"
           width="100%" height="100%">
    <mx:states>
        <mx:State name="hideLegend">
            <mx:RemoveChild target="{legend}"/>
        </mx:State>
    </mx:states>
    <mx:Script><![CDATA[
        import com.easyinsight.analysis.AnalysisDateDimension;
        import com.easyinsight.analysis.AnalysisDefinition;
        import com.easyinsight.analysis.AnalysisItem;
        import com.easyinsight.analysis.AnalysisItemTypes;
        import com.easyinsight.analysis.AnalysisMeasure;
        import com.easyinsight.analysis.CustomChangeEvent;
        import com.easyinsight.analysis.FillProvider;
        import com.easyinsight.analysis.MultiColor;
        import com.easyinsight.analysis.charts.ChartUtil;
        import com.easyinsight.analysis.charts.EILegend;
        import com.easyinsight.pseudocontext.PseudoContextWindow;

        import mx.charts.AreaChart;
        import mx.charts.AxisRenderer;
        import mx.charts.CategoryAxis;
        import mx.charts.DateTimeAxis;
        import mx.charts.HitData;
        import mx.charts.LinearAxis;
        import mx.charts.chartClasses.IAxis;
        import mx.charts.events.ChartItemEvent;
        import mx.charts.series.AreaSeries;
        import mx.charts.series.items.AreaSeriesItem;
        import mx.collections.ArrayCollection;
        import mx.collections.Sort;
        import mx.collections.SortField;
        import mx.graphics.SolidColor;
        import mx.graphics.Stroke;
        import mx.managers.PopUpManager;

        [Bindable]
        private var chartData:ArrayCollection;

        private var areaChart:AreaChart;

        private var xAxisItem:AnalysisItem;
        private var yAxisItem:AnalysisItem;
        private var measureItem:AnalysisItem;

        [Bindable]
        private var legend:EILegend;

        private var def:AreaChartDefinition;

        private function dataTipFunction(hitData:HitData):String {
            var string:String;
            var lineSeries:AreaSeries = hitData.element as AreaSeries;
            var lineSeriesItem:AreaSeriesItem = hitData.chartItem as AreaSeriesItem;
            if (def.multiMeasure) {
                for each (var aMeasure:AnalysisMeasure in def.measures) {
                    if (aMeasure.unqualifiedDisplay == lineSeries.displayName) {
                        string = lineSeries.displayName + "\n" +
                                xAxisItem.getFormatter().format(lineSeriesItem.xValue) + "\n" + aMeasure.getFormatter().format(lineSeriesItem.yValue);
                        break;
                    }
                }
            } else {
                string = lineSeries.displayName + "\n" +
                        xAxisItem.getFormatter().format(lineSeriesItem.xValue) + "\n" + measureItem.getFormatter().format(lineSeriesItem.yValue);
            }
            return string;
        }

        override protected function createChildren():void {
            super.createChildren();
            if (areaChart == null) {
                areaChart = new AreaChart();
                areaChart.percentHeight = 100;
                areaChart.percentWidth = 100;
            }
            hbox.addChild(areaChart);
            if (legend == null) {
                legend = new EILegend();
                legend.percentHeight = 100;
            }
            hbox.addChild(legend);
        }

        private function onClick(event:ChartItemEvent):void {

            var window:PseudoContextWindow = new PseudoContextWindow(yAxisItem, passThrough, this, areaChartDef, event.hitData.item);
            PopUpManager.addPopUp(window, this);
            window.x = event.stageX + 5;
            window.y = event.stageY + 5;
        }

        private function passThrough(event:Event):void {
            dispatchEvent(event);
        }

        private var areaChartDef:AreaChartDefinition;

        public function renderReport(dataSet:ArrayCollection, analysisDefinition:AnalysisDefinition, clientProcessorMap:Object, additionalProperties:Object):void {

            areaChartDef = analysisDefinition as AreaChartDefinition;

            this.chartData = new ArrayCollection(dataSet.toArray());

            hbox.removeChild(areaChart);

            areaChart = new AreaChart();
            areaChartDef.styleChart(areaChart);
            areaChart.addEventListener("legendDataChanged", function (event:Event):void {
                legend.dataProvider = areaChart.legendData;
            });
            areaChart.showDataTips = true;
            areaChart.dataTipFunction = dataTipFunction;
            areaChart.percentHeight = 100;
            areaChart.percentWidth = 100;
            areaChart.backgroundElements = ChartUtil.gridLine("horizontal");
            areaChart.type = areaChartDef.stackingType;
            legend.maxWidth = areaChartDef.legendMaxWidth;
            var verticalAxis:LinearAxis = new LinearAxis();
            if (areaChartDef.yAxisMinimumDefined) {
                LinearAxis(verticalAxis).minimum = areaChartDef.yAxisMininum;
            }
            if (areaChartDef.yAxisMaximumDefined) {
                LinearAxis(verticalAxis).maximum = areaChartDef.yAxisMaximum;
            }
            verticalAxis.baseAtZero = areaChartDef.baseAtZero == "true";
            if (areaChartDef.yAxisLabel != null && areaChartDef.yAxisLabel != "") {
                verticalAxis.title = areaChartDef.yAxisLabel;
            } else {
                if (!areaChartDef.multiMeasure && areaChartDef.measure != null) {
                    verticalAxis.title = areaChartDef.measure.unqualifiedDisplay;
                } else {
                    if (areaChartDef.measures.length == 1) {
                        verticalAxis.title = AnalysisItem(areaChartDef.measures.getItemAt(0)).unqualifiedDisplay;
                    } else {
                        verticalAxis.title = "Measures";
                    }
                }
            }
            var hAxisRenderer:AxisRenderer = new AxisRenderer();
            hAxisRenderer.setStyle("axisStroke", ChartUtil.axisStroke());
            hAxisRenderer.axis = verticalAxis;
            areaChart.verticalAxisRenderer = hAxisRenderer;
            areaChart.verticalAxis = verticalAxis;

            if (dataSet.length > 0) {

                if (areaChartDef.showLegend) {
                    currentState = "";
                } else {
                    currentState = "hideLegend";
                }

                this.def = areaChartDef;
                var xAxisDimension:AnalysisItem = areaChartDef.xaxis;

                xAxisItem = xAxisDimension;
                yAxisItem = areaChartDef.yaxis;
                measureItem = areaChartDef.measure;

                var sort:Sort = new Sort();
                var sortField:SortField = new SortField(xAxisDimension.qualifiedName(), true);
                sort.fields = [ sortField ];
                if (chartData.length > 0) {
                    var firstObj:Object = chartData.getItemAt(0);
                    var firstVal:Object = firstObj[xAxisDimension.qualifiedName()];
                    if (firstVal is Number) {
                        sortField.numeric = true;
                    }

                    chartData.sort = sort;
                    chartData.refresh();

                    if (firstVal is Number) {
                        var categories:ArrayCollection = new ArrayCollection();
                        for each (var obj:Object in chartData) {
                            var valObj:Object = obj[xAxisDimension.qualifiedName()];
                            if (categories.getItemIndex(valObj) == -1) {
                                categories.addItem(valObj);
                            }
                        }
                    }
                }


                var xAxis:IAxis;

                if (xAxisDimension.hasType(AnalysisItemTypes.DATE)) {
                    var dateDimension:AnalysisDateDimension = xAxisDimension as AnalysisDateDimension;
                    var dateAxis:DateTimeAxis = new DateTimeAxis();
                    switch (dateDimension.dateLevel) {
                        case AnalysisItemTypes.YEAR_LEVEL:
                            dateAxis.dataUnits = "years";
                            break;
                        case AnalysisItemTypes.MONTH_LEVEL:
                        case AnalysisItemTypes.QUARTER_OF_YEAR:
                            dateAxis.dataUnits = "months";
                            break;
                        case AnalysisItemTypes.DAY_LEVEL:
                            dateAxis.dataUnits = "days";
                            break;
                        case AnalysisItemTypes.HOUR_LEVEL:
                            dateAxis.dataUnits = "hours";
                            break;
                        case AnalysisItemTypes.MINUTE_LEVEL:
                            dateAxis.dataUnits = "minutes";
                            break;
                    }
                    dateAxis.displayName = dateDimension.unqualifiedDisplay;
                    if (additionalProperties["xAxisMinimum"] != null) {
                        dateAxis.minimum = additionalProperties["xAxisMinimum"];
                    }
                    if (additionalProperties["xAxisMaximum"] != null) {
                        dateAxis.maximum = additionalProperties["xAxisMaximum"];
                    }
                    dateAxis.title = xAxisDimension.display;
                    xAxis = dateAxis;
                } else {
                    var categoryAxis:CategoryAxis = new CategoryAxis();
                    categoryAxis.displayName = xAxisDimension.unqualifiedDisplay;
                    categoryAxis.dataProvider = categories;
                    categoryAxis.title = xAxisDimension.unqualifiedDisplay;
                    xAxis = categoryAxis;
                }
                var vAxisRenderer:AxisRenderer = new AxisRenderer();
                vAxisRenderer.setStyle("axisStroke", ChartUtil.axisStroke());
                vAxisRenderer.axis = xAxis;
                areaChart.horizontalAxisRenderer = vAxisRenderer;
                areaChart.horizontalAxis = xAxis;

                var mySeries:Array = new Array();
                var uniques:ArrayCollection = new ArrayCollection();

                var strokes:Array = [];
                var fills:Array = [];
                var useCustomColors:Boolean;
                var colors:Array;
                useCustomColors = (areaChartDef.multiColors != null && areaChartDef.multiColors.length && MultiColor(areaChartDef.multiColors.getItemAt(0)).color1StartEnabled);
                if (useCustomColors) {
                    colors = [];
                    for each (var mc:MultiColor in areaChartDef.multiColors) {
                        if (mc.color1StartEnabled) {
                            colors.push(new SolidColor(mc.color1Start));
                        }
                    }
                } else {
                    colors = FillProvider.createSolidColors();
                }

                for each (var color:SolidColor in colors) {
                    var newStroke:Stroke = new Stroke();
                    newStroke.color = color.color;
                    newStroke.alpha = 1;
                    strokes.push(newStroke);
                    fills.push(color);
                }

                var seriesData:Object = new Object();
                areaChart.dataProvider = areaChartDef.populateData(dataSet, seriesData, uniques);
                for (var i:int = 0; i < uniques.length; i++) {
                    var key:String = uniques.getItemAt(i) as String;
                    var uniqueLineSeries:AreaSeries = new AreaSeries();
                    uniqueLineSeries.setStyle("form", areaChartDef.form);
                    uniqueLineSeries.xField = areaChartDef.xaxis.qualifiedName();
                    if (key == null || key == "") {
                        key = "[ No Value ]";
                    }
                    uniqueLineSeries.yField = key;
                    uniqueLineSeries.setStyle("areaStroke", strokes[i % strokes.length]);
                    uniqueLineSeries.setStyle("areaFill", fills[i % fills.length]);
                    uniqueLineSeries.displayName = key;
                    mySeries.push(uniqueLineSeries);
                }

                areaChart.series = mySeries;
            }

            hbox.addChildAt(areaChart, 0);
        }

        public function updateExportMetadata(report:AnalysisDefinition = null):void {
        }

        public function preserveValues():Boolean {
            return false;
        }

        public function onCustomChangeEvent(event:CustomChangeEvent):void {
        }
        ]]></mx:Script>
    <mx:HBox width="100%" height="100%" id="hbox"/>
</mx:Module>