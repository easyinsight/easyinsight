<?xml version="1.0" ?>
<charts:CartesianChartModule xmlns:mx="http://www.adobe.com/2006/mxml"
                             implements="com.easyinsight.analysis.IReportRenderer"
                             width="100%" height="100%" xmlns:charts="com.easyinsight.analysis.charts.*"
        creationComplete="onCreation()">

    <mx:Script><![CDATA[
        import com.easyinsight.analysis.AnalysisDefinition;
        import com.easyinsight.analysis.AnalysisItem;

        import com.easyinsight.analysis.AnalysisMeasure;

        import com.easyinsight.analysis.ChartDefinition;

        import com.easyinsight.analysis.CustomChangeEvent;
        import com.easyinsight.analysis.SortFunctionFactory;

        import com.easyinsight.analysis.charts.Chart3DVisuals;

        import ilog.charts3d.BarChart3D;

        import ilog.charts3d.charts3dClasses.CartesianChart3D;
        import ilog.charts3d.series.BarSeries3D;
        import ilog.charts3d.series.items.BarSeries3DItem;

        import mx.charts.CategoryAxis;
        import mx.charts.ChartItem;
        import mx.charts.HitData;
        import mx.charts.LinearAxis;
        import mx.charts.chartClasses.CartesianTransform;
        import mx.charts.chartClasses.ChartBase;
        import mx.charts.chartClasses.DataTransform;
        import mx.charts.chartClasses.Series;
        import mx.collections.ArrayCollection;
        import mx.collections.Sort;

        private var visuals:Chart3DVisuals = new Chart3DVisuals();

        private function onCreation():void {
            visuals.addEventListener(CustomChangeEvent.CUSTOM_CHANGE, onCustomChange);
        }

        private function onCustomChange(event:CustomChangeEvent):void {
            dispatchEvent(event);
        }

        override protected function getDimensionItem():AnalysisItem {
            return Bar3DChartDefinition(chartDef).yaxis;
        }

        override protected function getMeasures():ArrayCollection {
            return Bar3DChartDefinition(chartDef).measures;
        }

        override protected function createChartObject():ChartBase {
            var chart:BarChart3D = new BarChart3D();
            visuals.chart = chart;
            return chart;
        }

        override protected function useChartColor():Boolean {
            return Bar3DChartDefinition(chartDef).useChartColor;
        }

        override protected function getChartColor():uint {
            return Bar3DChartDefinition(chartDef).chartColor;
        }

        override protected function getColorScheme():String {
            return Bar3DChartDefinition(chartDef).colorScheme;
        }

        override public function renderReport(dataSet:ArrayCollection, analysisDefinition:AnalysisDefinition, clientProcessorMap:Object, additionalProperties:Object):void {
            super.renderReport(dataSet, analysisDefinition, clientProcessorMap, additionalProperties);
            var chartDef:Bar3DChartDefinition = analysisDefinition as Bar3DChartDefinition;
            if (chartDef.elevationAngle > 0) {
                visuals.elevationAngle = chartDef.elevationAngle;
                visuals.rotationAngle = chartDef.rotationAngle;
            }
        }

        override protected function sortData(dataSet:ArrayCollection):void {
            var columnChartDef:Bar3DChartDefinition = chartDef as Bar3DChartDefinition;
            var firstMeasure:AnalysisMeasure = columnChartDef.measures.getItemAt(0) as AnalysisMeasure;
            if (columnChartDef.columnSort != ChartDefinition.SORT_UNSORTED) {
                var sort:Sort = new Sort();
                if (columnChartDef.columnSort == ChartDefinition.SORT_X_ASCENDING) {
                    sort.compareFunction = SortFunctionFactory.createSortFunction(getDimensionItem(), false);
                } else if (columnChartDef.columnSort == ChartDefinition.SORT_X_DESCENDING) {
                    sort.compareFunction = SortFunctionFactory.createSortFunction(getDimensionItem(), true);
                } else if (columnChartDef.columnSort == ChartDefinition.SORT_Y_ASCENDING) {
                    sort.compareFunction = SortFunctionFactory.createSortFunction(firstMeasure, false);
                } else if (columnChartDef.columnSort == ChartDefinition.SORT_Y_DESCENDING) {
                    sort.compareFunction = SortFunctionFactory.createSortFunction(firstMeasure, true);
                }
                dataSet.sort = sort;
                dataSet.refresh();
            }
        }

        override protected function formatDataTip(hd:HitData):String {
            var dt:String = "";
            var barSeriesItem:BarSeries3DItem = hd.chartItem as BarSeries3DItem;
            var series:Series = barSeriesItem.element as Series;
            var dataTransform:DataTransform = series.dataTransform;
            var n:String = series.displayName;
            if (n != null && n.length > 0)
                dt += "<b>" + n + "</b><BR/>";

            var xName:String = dataTransform.getAxis(CartesianTransform.VERTICAL_AXIS).displayName;
            if (xName != "")
                dt += "<i>" + xName + ":</i> ";
            dt += dataTransform.getAxis(CartesianTransform.VERTICAL_AXIS).formatForScreen(BarSeries3DItem(hd.chartItem).yValue) + "\n";

            var yName:String = dataTransform.getAxis(CartesianTransform.HORIZONTAL_AXIS).displayName;

            if (yName != "")
                dt += "<i>" + yName + ":</i> ";
            dt += measureFormatter.format(BarSeries3DItem(hd.chartItem).xValue) + "\n";

            return dt;
        }

        override protected function getAngle():int {
            return 90;
        }

        override protected function assignDimensionAxis(axis:CategoryAxis, chart:ChartBase):void {
            CartesianChart3D(chart).verticalAxis = axis;
        }

        override protected function assignMeasureAxis(axis:LinearAxis, chart:ChartBase):void {
            CartesianChart3D(chart).horizontalAxis = axis;
        }

        override protected function createSeries(measure:AnalysisMeasure, dataSet:ArrayCollection, xField:String, measureNumber:int):Series {
            var barSeries:BarSeries3D = new BarSeries3D();
            barSeries.yField = xField;
            barSeries.xField = measure.qualifiedName();
            barSeries.labelFunction = function (element:ChartItem, series:Series):String {
                var barSeriesItem:BarSeries3DItem = element as BarSeries3DItem;
                return measure.getFormatter().format(barSeriesItem.xNumber);
            };
            barSeries.dataProvider = dataSet;
            barSeries.dataFunction = function(series:Series, item:Object, fieldName:String):Object {
                if (fieldName == 'xValue')
                    return(item[measure.qualifiedName()].toNumber());
                else if (fieldName == "yValue")
                    return(item[xField].toString());
                else
                    return null;
            };
            barSeries.displayName = measure.display;
            barSeries.setStyle("showDataEffect", interpolateIn);
            barSeries.setStyle("hideDataEffect", interpolateIn);
            return barSeries;
        }
        ]]></mx:Script>
    <mx:SeriesSlide id="interpolateIn" duration="1000" direction="up"/>
    <mx:SeriesSlide id="interpolateOut" duration="1000" direction="down"/>
</charts:CartesianChartModule>