<?xml version="1.0" ?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" implements="com.easyinsight.analysis.IReportRenderer"
        width="100%" height="100%" creationComplete="onCreation()">
    <mx:Script>
		<![CDATA[
        import com.easyinsight.analysis.AnalysisDefinition;
        import com.easyinsight.analysis.CustomChangeEvent;
        import com.easyinsight.analysis.charts.Chart3DVisuals;
import com.easyinsight.filtering.FilterRawData;

        import com.easyinsight.pseudocontext.PseudoContextWindow;

        import ilog.charts3d.charts3dClasses.Series3D;
        import ilog.charts3d.series.BarSeries3D;
        import ilog.charts3d.series.BarSet3D;
        import ilog.charts3d.BarChart3D;

        import com.easyinsight.analysis.AnalysisItem;

        import mx.binding.utils.BindingUtils;
        import mx.charts.events.ChartItemEvent;
        import mx.events.FlexEvent;
        import mx.graphics.BitmapFill;
        import mx.graphics.IFill;        

        import mx.charts.chartClasses.IAxis;
        import mx.charts.LinearAxis;
        import mx.formatters.Formatter;
        import mx.charts.series.items.BarSeriesItem;
        import mx.charts.chartClasses.Series;
        import mx.charts.ChartItem;
        import mx.charts.CategoryAxis;
        import mx.collections.ArrayCollection;
        import mx.managers.PopUpManager;

        [Bindable]
        private var graphData:ArrayCollection = new ArrayCollection();

        [Bindable]
        private var xAxisTitle:String = "";

        [Bindable]
        private var yAxisTitle:String = "";

        [Bindable]
        private var xAxisItem:AnalysisItem;
        [Bindable]
        private var yAxisItem:AnalysisItem;

        [Bindable]
        private var sortable:Boolean;

        [Bindable]
        private var xField:String;
        private var _yField:String;

        private var dimensionFormatter:Formatter;

        private var visuals:Chart3DVisuals = new Chart3DVisuals();

        [Bindable]
        public function get yField():String {
            return _yField;
        }

        public function set yField(val:String):void {
            _yField = val;
            dispatchEvent(new FlexEvent(FlexEvent.DATA_CHANGE));
        }

        private function onCreation():void {
            visuals.addEventListener(CustomChangeEvent.CUSTOM_CHANGE, onCustomChange);
        }

        private function onCustomChange(event:CustomChangeEvent):void {
            dispatchEvent(event);
        }

        private var measureFormatter:Formatter;

        private var customColors:Boolean = false;

        private var barChart:BarChart3D;

        private var xAxis:LinearAxis;
        private var yAxis:CategoryAxis;

        private var xAxisTitleRendererFactory:ClassFactory;

        private var yAxisTitleRendererFactory:ClassFactory;

        private var barSeries:BarSeries3D;

        private var bgFills:Array;

        [Bindable]
        [Embed(source="../../../../../../../assets/water4.JPG")]
        private var standardFill:Class;

        override protected function createChildren():void {
            super.createChildren();
            bgFills = [];
            for (var i:int = 0; i < 15; i++) {
                var bgFill:BitmapFill = new BitmapFill();
                bgFill.source = standardFill;
                bgFill.originX = Math.random() * 60;
                bgFill.originY = Math.random() * 60;
                bgFills.push(bgFill);
            }
        }

        private function createChart():void {
            if (barChart == null) {

                // define the chart itself

                barChart = new BarChart3D();
                barChart.percentHeight = 100;
                barChart.percentWidth = 100;
                barChart.showDataTips = true;
                barChart.dataProvider = graphData;
                visuals.chart = barChart;

                // define the X axis

                yAxis = new CategoryAxis();
                BindingUtils.bindProperty(yAxis, "categoryField", this, "yField");
                yAxis.dataProvider = graphData;
                yAxis.labelFunction = renderDimensionAxis;
                yAxis.title = yAxisTitle;
                barChart.verticalAxis = yAxis;
                /*var axisRenderer:AxisRenderer = new AxisRenderer();
                 axisRenderer.axis = yAxis;
                 yAxisTitleRendererFactory = new SortableAxisTitleFactory(yAxisItem, false);
                 axisRenderer.titleRenderer = yAxisTitleRendererFactory;
                 axisRenderer.placement = "left";
                 barChart.verticalAxisRenderers = [ axisRenderer ];*/

                // define the Y axis

                xAxis = new LinearAxis();
                xAxis.title = xAxisTitle;
                xAxis.labelFunction = renderAxis;
                barChart.horizontalAxis = xAxis;
                /*var xAxisRenderer:AxisRenderer = new AxisRenderer();
                 xAxisRenderer.axis = xAxis;
                 xAxisTitleRendererFactory = new SortableAxisTitleFactory(xAxisItem, true);
                 xAxisRenderer.titleRenderer = xAxisTitleRendererFactory;
                 xAxisRenderer.placement = "bottom";
                 barChart.horizontalAxisRenderers = [ xAxisRenderer ];*/

                // create the data series

                barSeries = new BarSeries3D();
                barSeries.setStyle("stroke", s1);
                barSeries.fillFunction = customFill;
                barSeries.xField = xField;
                barSeries.yField = yField;
                barSeries.labelFunction = renderChartLabel;
                barSeries.dataProvider = graphData;
                barSeries.displayName = yAxisTitle;
                barSeries.setStyle("showDataEffect", interpolateIn);
                barSeries.dataFunction = myDataFunction;
                var barSet:BarSet3D = new BarSet3D();
                barSet.series = [ barSeries ];
                barChart.series = [ barSet ];
                barChart.addEventListener(ChartItemEvent.ITEM_CLICK, onClick);
                canvas.addChild(barChart);
            } else {
                barChart.visible = true;
            }
        }

        private function myDataFunction(series:Series3D, item:Object, fieldName:String):Object {
              if(fieldName == 'xValue')
                  return(item[xAxisItem.qualifiedName()].toNumber());
              else if(fieldName == "yValue")
                  return(item[yAxisItem.qualifiedName()].toString());
              else
                  return null;
         }

        private function onClick(event:ChartItemEvent):void {

                var window:PseudoContextWindow = new PseudoContextWindow(yAxisItem, passThrough, this, barChartDef);
                window.data = event.hitData.item;
                PopUpManager.addPopUp(window, this);
                window.x = event.stageX + 5;
                window.y = event.stageY + 5;
        }

        private function passThrough(event:Event):void {
            dispatchEvent(event);
        }

        private var barChartDef:Bar3DChartDefinition;

        public function renderReport(dataSet:ArrayCollection, analysisDefinition:AnalysisDefinition, clientProcessorMap:Object, additionalProperties:Object):void {
            barChartDef = analysisDefinition as Bar3DChartDefinition;


            if (dataSet.length > 0) {
                stackIndex = 0;
                yField = barChartDef.yaxis.qualifiedName();
                createChart();

                if (barChartDef.elevationAngle > 0) {
                    visuals.elevationAngle = barChartDef.elevationAngle;
                    visuals.rotationAngle = barChartDef.rotationAngle;
                }

                measureFormatter = barChartDef.measure.getFormatter();
                dimensionFormatter = barChartDef.yaxis.getFormatter();

                var newXAxisItem:AnalysisItem = barChartDef.measure;
                if (xAxisItem == null || newXAxisItem.qualifiedName() != xAxisItem.qualifiedName()) {
                    xAxisItem = newXAxisItem;
                    if (sortable) {
                        //xAxisTitleRendererFactory.analysisItem = xAxisItem;
                    }
                }

                var newYAxisItem:AnalysisItem = barChartDef.yaxis;
                if (yAxisItem == null || newYAxisItem.qualifiedName() != yAxisItem.qualifiedName()) {
                    yAxisItem = newYAxisItem;
                    if (sortable) {
                        //yAxisTitleRendererFactory.analysisItem = yAxisItem;
                    }
                }

                var newXField:String = newXAxisItem.qualifiedName();
                if (newXField != xField) {
                    xField = newXField;
                    barSeries.xField = xField;
                }

                var newXAxisTitle:String = newXAxisItem.display;
                if (newXAxisTitle != xAxisTitle) {
                    xAxisTitle = newXAxisTitle;
                    xAxis.title = xAxisTitle;
                }

                var newYField:String = newYAxisItem.qualifiedName();
                if (newYField != yField) {
                    yField = newYField;
                    yAxis.categoryField = yField;
                    barSeries.yField = yField;
                }

                var newYAxisTitle:String = newYAxisItem.display;
                if (newYAxisTitle != yAxisTitle) {
                    yAxisTitle = newYAxisTitle;
                    yAxis.title = yAxisTitle;
                    barSeries.displayName = yAxisTitle;
                }
                barChart.dataProvider = dataSet;
            barSeries.dataProvider = dataSet;
            yAxis.dataProvider = dataSet;
            } else {
                if (barChartDef.measure != null && barChartDef.yaxis != null) stackIndex = 2;
                else stackIndex = 1;
                if (barChart != null) {
                    barChart.dataProvider = new ArrayCollection();
                    barSeries.dataProvider = new ArrayCollection();
                    yAxis.dataProvider = new ArrayCollection();
                    barChart.visible = false;
                }
            }


            graphData = dataSet;
        }

        private function renderChartLabel(element:ChartItem, series:Series):String {
            var barSeriesItem:BarSeriesItem = element as BarSeriesItem;
            return measureFormatter.format(barSeriesItem.xNumber);
        }

        private function customFill(element:ChartItem, index:Number):IFill {
            return bgFills[index % 15];
        }

        private function renderAxis(labelValue:Object, previousValue:Object, axis:IAxis):String {
            return measureFormatter.format(labelValue);
        }

        private function renderDimensionAxis(item:Object, prevValue:Object, axis:CategoryAxis, categoryItem:Object):String {
            return dimensionFormatter.format(item);
        }

        public function createFilterRawData():FilterRawData {
            var filterRawData:FilterRawData = new FilterRawData();
            for (var i:int = 0; i < barChart.selectedChartItems.length; i++) {
                var obj:ChartItem = barChart.selectedChartItems[i];
                filterRawData.addPair(xAxisItem, obj.item[xAxisItem.key.createString()]);
            }
            return filterRawData;
        }

        [Bindable]
        private var stackIndex:int = 0;

        public function updateExportMetadata():void {
        }

        public function preserveValues():Boolean {
            return true;
        }

        public function onCustomChangeEvent(event:CustomChangeEvent):void {
        }
		]]>
	</mx:Script>
    <mx:Stroke id="s1" color="#2A7E93" weight="2"/>
	<mx:SeriesInterpolate id="interpolateIn" duration="1000"/>
    <mx:Canvas width="100%" height="100%" id="canvas">
        <mx:ViewStack width="100%" height="100%" selectedIndex="{stackIndex}" creationPolicy="all" mouseChildren="false"
                      mouseEnabled="false">
            <mx:Canvas width="100%" height="100%"/>
            <mx:Box width="100%" height="100%" horizontalAlign="center" verticalAlign="middle" backgroundColor="#000000"
                    backgroundAlpha=".1">
                <mx:Box borderStyle="solid" borderThickness="1" cornerRadius="10" horizontalAlign="center"
                        verticalAlign="middle"
                        backgroundColor="#FFFFFF" paddingBottom="15" paddingLeft="15" paddingRight="15" paddingTop="15">
                    <mx:TextArea fontSize="22"
                                 text="You haven't set up a report yet. Drag one Grouping and one Measure into the labeled areas above the canvas to create a report."
                                 borderStyle="none" backgroundAlpha="0" width="500" height="95"/>
                </mx:Box>
            </mx:Box>
            <mx:Box width="100%" height="100%" horizontalAlign="center" verticalAlign="middle" backgroundColor="#000000"
                    backgroundAlpha=".1">
                <mx:Box borderStyle="solid" borderThickness="1" cornerRadius="10" horizontalAlign="center"
                        verticalAlign="middle"
                        backgroundColor="#FFFFFF" paddingBottom="15" paddingLeft="15" paddingRight="15" paddingTop="15">
                    <mx:TextArea fontSize="22"
                                 text="We didn't find any data for the fields and filters that you specified in the report."
                                 borderStyle="none" backgroundAlpha="0" width="500" height="65"/>
                </mx:Box>
            </mx:Box>
        </mx:ViewStack>
    </mx:Canvas>
</mx:Module>