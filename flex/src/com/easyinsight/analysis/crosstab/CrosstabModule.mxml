<?xml version="1.0" ?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:crosstab="com.easyinsight.analysis.crosstab.*" width="100%"
           height="100%" implements="com.easyinsight.analysis.IReportRenderer" horizontalScrollPolicy="off">
    <mx:states>
        <mx:State name="notConfigured">
            <mx:RemoveChild target="{dataGrid}"/>
            <mx:AddChild relativeTo="{box}">
                <mx:Box width="100%" height="100%" horizontalAlign="center" verticalAlign="middle" backgroundColor="#000000" backgroundAlpha=".1">
                    <mx:Box borderStyle="solid" borderThickness="1" cornerRadius="10" horizontalAlign="center" verticalAlign="middle"
                            backgroundColor="#FFFFFF" paddingBottom="15" paddingLeft="15" paddingRight="15" paddingTop="15">
                        <mx:TextArea fontSize="22" text="You haven't set up a report yet. You can drag the fields from the left into this canvas or into the drop areas above to get started!"
                            borderStyle="none" backgroundAlpha="0" width="500" height="95" editable="false" selectable="false" focusEnabled="false"/>
                    </mx:Box>
                </mx:Box>
            </mx:AddChild>
        </mx:State>
        <mx:State name="noData">
            <mx:RemoveChild target="{dataGrid}"/>
            <mx:AddChild relativeTo="{box}">
                <mx:Box width="100%" height="100%" horizontalAlign="center" verticalAlign="middle" backgroundColor="#000000" backgroundAlpha=".1">
                    <mx:Box borderStyle="solid" borderThickness="1" cornerRadius="10" horizontalAlign="center" verticalAlign="middle"
                            backgroundColor="#FFFFFF" paddingBottom="15" paddingLeft="15" paddingRight="15" paddingTop="15">
                        <mx:TextArea fontSize="22" text="We didn't find any data for the fields and filters that you specified in the report."
                            borderStyle="none" backgroundAlpha="0" width="500" height="65" editable="false" selectable="false" focusEnabled="false"/>
                    </mx:Box>
                </mx:Box>
            </mx:AddChild>
        </mx:State>
    </mx:states>
    <mx:Script><![CDATA[
        import com.easyinsight.analysis.AnalysisDefinition;
        import com.easyinsight.analysis.AnalysisItem;
        import com.easyinsight.analysis.AnalysisMeasure;
        import com.easyinsight.analysis.CustomChangeEvent;

        import mx.collections.ArrayCollection;
        import mx.controls.Alert;
        import mx.controls.advancedDataGridClasses.AdvancedDataGridColumn;
        import mx.formatters.Formatter;

        private var dimension1:AnalysisItem;

        private var crosstabDefinition:CrosstabDefinition;

        public function renderReport(data:ArrayCollection, analysisDefinition:AnalysisDefinition, clientProcessorMap:Object, additionalProperties:Object):void {
            crosstabDefinition = analysisDefinition as CrosstabDefinition;
            if (data.length > 3) {
                var translated:ArrayCollection = new ArrayCollection();
                for each (var row:CrosstabMapWrapper in data) {
                    translated.addItem(row.map);
                }
                var columnCount:int = additionalProperties["columnCount"];
                var columns:Array = [];

                var measureFormatter:Formatter = AnalysisMeasure(crosstabDefinition.measures.getItemAt(0)).getFormatter();

                for (var i:int = 0; i < columnCount; i++) {
                    var maxWidth:int = 0;
                    for each (var testRow:Object in translated) {
                        var crosstabValue:CrosstabValue = testRow[String(i)];
                        if (crosstabValue == null) {
                            continue;
                        }
                        var text:String;
                        if (crosstabValue.header == null) {
                            text = measureFormatter.format(crosstabValue.value);
                        } else if (crosstabValue.headerLabel) {
                            text = String(crosstabValue.value.getValue());
                        } else {
                            text = crosstabValue.header.getFormatter().format(crosstabValue.value);
                        }
                        var metrics:TextLineMetrics = dataGrid.measureText(text);
                        maxWidth = Math.max(metrics.width + metrics.x + metrics.x + 4, maxWidth);
                        maxWidth = Math.min(maxWidth, 300);
                    }
                    var column:AdvancedDataGridColumn = new AdvancedDataGridColumn();
                    var factory:ClassFactory = new ClassFactory(CrosstabCellRenderer);
                    factory.properties = { formatter: measureFormatter, cellProperty: String(i), report: crosstabDefinition };
                    column.dataField = String(i);
                    column.width = maxWidth;
                    column.itemRenderer = factory;
                    columns.push(column);
                }

                dataGrid.headerRows = crosstabDefinition.rows.length;
                dataGrid.rowItems = crosstabDefinition.columns.length + 1;
                /*dataGrid.lockedColumnCount = crosstabDefinition.rows.length;
                dataGrid.lockedRowCount = crosstabDefinition.columns.length + 1;*/
                dataGrid.columns = columns;
                var reportWidth:int = 1;
                for each (var col:AdvancedDataGridColumn in columns) {
                    reportWidth += (col.width + 1);
                }
                var endWidth:int = 0;
                if (reportWidth < this.width) {
                    var delta:int = (this.width - reportWidth);
                    delta = Math.min(delta, 300);
                    var perColumnAdd:int = (delta) / columns.length;
                    for each (var colAdd:AdvancedDataGridColumn in columns) {
                        colAdd.width += perColumnAdd;
                        endWidth += colAdd.width;
                    }
                }
                //dataGrid.maxWidth = this.width;
                if (endWidth > 0) {
                    dataGrid.x = (this.width - endWidth) / 2;
                } else {
                    dataGrid.x = 0;
                }
                dataGrid.height = translated.length * 22 + 4;
                dataGrid.dataProvider = translated;
                currentState = "";
            } else {
                if (crosstabDefinition.columns.length == 0 || crosstabDefinition.rows.length == 0 || crosstabDefinition.measures.length == 0) {
                    currentState = "notConfigured";
                } else {
                    currentState = "noData";
                }
            }
        }

        public function updateExportMetadata():void {
        }

        private function passThrough(event:Event):void {
            dispatchEvent(event);
        }

        public function preserveValues():Boolean {
            return false;
        }

        public function onCustomChangeEvent(event:CustomChangeEvent):void {
        }]]></mx:Script>
    <mx:Canvas id="box" verticalScrollPolicy="auto" horizontalScrollPolicy="auto" width="100%" height="100%">
        <crosstab:CrosstabDataGrid id="dataGrid" fontFamily="Tahoma" horizontalGridLines="true" verticalGridLines="true" headerHeight="0"
            rowHeight="22" horizontalScrollPolicy="off"/>
    </mx:Canvas>
</mx:Module>