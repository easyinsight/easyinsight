<?xml version="1.0"?>
<util:EISlimWindow xmlns:mx="http://www.adobe.com/2006/mxml"
                   creationComplete="onCreation()" xmlns:util="com.easyinsight.util.*">
    <mx:Script><![CDATA[
        import com.easyinsight.quicksearch.EIDescriptor;
        import com.easyinsight.util.ConnectionAddDescWindow;
        import com.easyinsight.util.ConnectionDescriptorEvent;
        import com.easyinsight.util.PopUpUtil;
        import com.easyinsight.util.ProgressAlert;

        import mx.collections.ArrayCollection;
        import mx.controls.Alert;
        import mx.managers.PopUpManager;

        public var dataSourceType:int;

        private function onCreation():void {
            ProgressAlert.alert(this, "Retrieving...", null, adminService.getConnectionReports);
            adminService.getConnectionReports.send(dataSourceType);
        }

        private function saved():void {
            PopUpManager.removePopUp(this);
        }

        private function save():void {
            ProgressAlert.alert(this, "Saving...", null, adminService.updateConnectionReports);
            adminService.updateConnectionReports.send(dataSourceType, reports);
        }

        [Bindable]
        private var reports:ArrayCollection;

        private function gotReports():void {
            var reports:ArrayCollection = adminService.getConnectionReports.lastResult as ArrayCollection;
            var vReports:ArrayCollection = new ArrayCollection();
            for each (var desc:EIDescriptor in reports) {
                vReports.addItem(desc);
            }
            this.reports = vReports;
        }

        private function onAdd(event:ConnectionDescriptorEvent):void {
            reports.addItem(event.desc);
        }

        private function addReport():void {
            var window:ConnectionAddDescWindow = new ConnectionAddDescWindow();
            window.dataSourceType = dataSourceType;
            window.addEventListener(ConnectionDescriptorEvent.CONNECTION_ADD, onAdd, false, 0, true);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function removeSelected():void {
            var selected:ArrayCollection = new ArrayCollection();
            for each (var desc:EIDescriptor in reports) {
                if (desc.selected) {
                    selected.addItem(desc);
                }
            }
            for each (var d:EIDescriptor in selected) {
                reports.removeItemAt(reports.getItemIndex(d));
            }
        }

        private var removed:ArrayCollection = new ArrayCollection();
        ]]></mx:Script>
    <mx:RemoteObject destination="admin" id="adminService">
        <mx:method name="getConnectionReports" result="gotReports()"/>
        <mx:method name="updateConnectionReports" result="saved()"/>
    </mx:RemoteObject>
    <mx:HBox>
        <util:SaveButton label="Add..." click="addReport()" fontSize="12"/>
        <util:SaveButton label="Remove Selected" click="removeSelected()" fontSize="12"/>
    </mx:HBox>
    <mx:DataGrid dataProvider="{reports}" wordWrap="true" variableRowHeight="true" height="450" id="dataGrid">
        <mx:columns>
            <mx:DataGridColumn itemRenderer="com.easyinsight.util.GenericGridCheckbox" width="30" headerText=""/>
            <mx:DataGridColumn headerText="Field" dataField="name" width="400"/>
        </mx:columns>
    </mx:DataGrid>
    <mx:HBox width="100%" horizontalAlign="center">
        <util:SaveButton label="Save" click="save()"/>
        <util:CancelButton label="Cancel" click="PopUpManager.removePopUp(this)"/>
    </mx:HBox>
</util:EISlimWindow>
