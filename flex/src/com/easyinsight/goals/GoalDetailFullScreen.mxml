<?xml version="1.0" ?>
<FullScreenPage xmlns:mx="http://www.adobe.com/2006/mxml" xmlns="com.easyinsight.*"
                xmlns:goals="com.easyinsight.goals.*" width="100%" height="100%">
    <mx:Script><![CDATA[
        import com.adobe.ac.mxeffects.CubeRotate;
        import com.easyinsight.analysis.AnalysisCloseEvent;
        import com.easyinsight.solutions.InsightDescriptor;

        import mx.collections.ArrayCollection;
        import mx.core.Container;
        import mx.events.ItemClickEvent;
        import com.easyinsight.dashboard.DataViewPanel;

        private function close():void {
            dispatchEvent(new AnalysisCloseEvent(this));
        }

        [Bindable]
        private var _goalTreeNodeData:GoalTreeNodeData;

        override protected function createChildren():void {
            super.createChildren();

            var topElements:ArrayCollection = new ArrayCollection();
            for each (var report:InsightDescriptor in _goalTreeNodeData.associatedInsights) {
                var reportItem:GoalDetailItem = new GoalDetailItem();
                reportItem.label = report.name;
                var dataViewPanel:DataViewPanel = new DataViewPanel();
                dataViewPanel.analysisID = report.id;
                dataViewPanel.reportName = report.name;
                dataViewPanel.reportType = report.reportType;
                dataViewPanel.dataSourceID = report.dataFeedID;
                reportItem.displayObject = dataViewPanel;
                viewStack.addChild(dataViewPanel);
                topElements.addItem(reportItem);
            }

            if (_goalTreeNodeData.associatedFeeds.length > 0) {
                var dataSourceItem:GoalDetailItem = new GoalDetailItem();
                dataSourceItem.label = "Data Sources";
                var goalDetailDataSources:GoalDetailDataSources = new GoalDetailDataSources();
                goalDetailDataSources.goalTreeNodeData = _goalTreeNodeData;
                dataSourceItem.displayObject = goalDetailDataSources;
                viewStack.addChild(goalDetailDataSources);
                topElements.addItem(dataSourceItem);
            }
            buttonBar.dataProvider = topElements;
        }

        private function choseButton(event:ItemClickEvent):void {
            var item:GoalDetailItem = event.item as GoalDetailItem;
            if (item.displayObject != viewStack.selectedChild) {
                var cubeRotate:CubeRotate = new CubeRotate();
                if (event.index > viewStack.selectedIndex) {
                    cubeRotate.direction = "RIGHT";
                } else {
                    cubeRotate.direction = "LEFT";
                }
                cubeRotate.duration = 1000;
                cubeRotate.target = viewStack.selectedChild;
                cubeRotate.siblings = [ item.displayObject ];
                viewStack.selectedChild = item.displayObject as Container;
                cubeRotate.play();
            }
        }

        public function set goalTreeNodeData(val:GoalTreeNodeData):void {
            _goalTreeNodeData = val;
        }]]></mx:Script>
    <mx:VBox width="100%" height="100%">
        <mx:HBox>
            <mx:Button id="wrapButton" icon="@Embed(source='../../../../assets/document_out.png')" click="close()" toolTip="Close"/>
        </mx:HBox>
        <mx:HBox width="100%" horizontalAlign="center">
            <mx:ButtonBar id="buttonBar" itemClick="choseButton(event)"/>
        </mx:HBox>
        <mx:ViewStack id="viewStack" width="100%" height="100%"/>
    </mx:VBox>
</FullScreenPage>