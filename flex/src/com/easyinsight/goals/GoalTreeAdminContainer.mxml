<?xml version="1.0" encoding="utf-8"?>
<mx:Module xmlns="com.easyinsight.*" xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:goals="com.easyinsight.goals.*"
	width="100%" height="100%" backgroundColor="#DCE2F8" creationComplete="onCreation()"
        implements="com.easyinsight.listing.IPerspective">
    <mx:Style>
        Accordion {
           headerHeight: 17;
           borderStyle: solid;
           backgroundAlpha: 1;
           focusAlpha: 0;
           highlightAlphas: 0.79, 0.2;
           fillAlphas: 1, 1, 1, 1;
           fillColors: #e0e0e0, #ffffff, #e0e0e0, #ffffff;
           selectedFillColors: #195eb9, #adf8ff;
           borderColor: #8e8e8e;
           color: #000000;
           textRollOverColor: #000000;
           textSelectedColor: #000000;
           fontSize: 10;
           fontWeight: normal;
        }
    </mx:Style>
	<mx:Script>
		<![CDATA[
        import com.easyinsight.administration.sharing.UserStub;
        import com.easyinsight.analysis.PromptEvent;
        import com.easyinsight.analysis.SavePromptWindow;
        import com.easyinsight.framework.User;
        import com.easyinsight.genredata.AnalyzeEvent;
        import com.easyinsight.util.PopUpUtil;
        import com.easyinsight.util.ProgressAlert;

        import com.easyinsight.analysis.AnalysisCloseEvent;

        import mx.managers.BrowserManager;
        import mx.managers.PopUpManager;
        import mx.utils.URLUtil;


        private function viewData():void {
            if (changed) {
                createPromptWindow("You have unsaved changes to this KPI tree.", function():void {
                    dispatchEvent(new AnalyzeEvent(new GoalDataAnalyzeSource(_goalTreeID)));
                });
            } else {
                dispatchEvent(new AnalyzeEvent(new GoalDataAnalyzeSource(_goalTreeID)));
            }
        }

        [Bindable]
        private var _goalTree:GoalTree;
        [Bindable]
        private var _goalTreeID:int;
        [Bindable]
        private var rootNode:GoalTreeNode;

        [Bindable]
        private var saved:Boolean;

        private var changed:Boolean = false;

        public function set goalTreeID(val:int):void {
            _goalTreeID = val;
        }

        private var _startDataSourceID:int = 0;

        public function set startDataSourceID(value:int):void {
            _startDataSourceID = value;
        }

        private var _dataSourceID:int = 0;

        [Bindable(event="dataSourceIDChanged")]
        public function get dataSourceID():int {
            return _dataSourceID;
        }

        public function set dataSourceID(value:int):void {
            if (_dataSourceID == value) return;
            _dataSourceID = value;
            dispatchEvent(new Event("dataSourceIDChanged"));
        }

        private function onCreation():void {
            addEventListener(GoalTreeChangedEvent.GOAL_TREE_CHANGED, onTreeChanged);
        }

        private function choseDataSource(event:KPITreeSetupEvent):void {
            dataSourceID = event.dataSource.id;
            _goalTree.dataSourceID = dataSourceID;
        }

        private function onTreeChanged(event:GoalTreeChangedEvent):void {
            changed = true;
        }

        override protected function createChildren():void {
            super.createChildren();
            if (_goalTreeID == 0) {
                saved = false;
                changed = true;
                _goalTree = new GoalTree();
                _goalTree.rootNode = new GoalTreeNode();
                var userStub:UserStub = new UserStub();
                userStub.userID = User.getInstance().userID;
                userStub.name = User.getInstance().userName;
                _goalTree.administrators.addItem(userStub);
                rootNode = _goalTree.rootNode;
                var feedFragmentObject:Object = new Object();
                feedFragmentObject.newGoalTree = "true";
                var feedFragmentString:String = URLUtil.objectToString(feedFragmentObject);
                BrowserManager.getInstance().setFragment(feedFragmentString);
                if (_startDataSourceID == 0) {
                    var window:KPITreeSetupWindow = new KPITreeSetupWindow();
                    window.addEventListener(KPITreeSetupEvent.KPI_TREE_SETUP, choseDataSource);
                    PopUpManager.addPopUp(window, this, true);
                    PopUpUtil.centerPopUp(window);
                } else {
                    dataSourceID = _startDataSourceID;
                    _goalTree.dataSourceID = dataSourceID;
                }
            } else {
                saved = true;
                ProgressAlert.alert(this, "Retrieving goal tree...", null, goalService.getGoalTree);
                goalService.getGoalTree.send(_goalTreeID);
            }
        }

        private function gotGoalTree():void {
            _goalTree = goalService.getGoalTree.lastResult as GoalTree;
            dataSourceID = _goalTree.dataSourceID;
            rootNode = _goalTree.rootNode;

            var feedFragmentObject:Object = new Object();
            feedFragmentObject.goalTreeAdminID = String(_goalTree.urlKey);
            var feedFragmentString:String = URLUtil.objectToString(feedFragmentObject);
            BrowserManager.getInstance().setFragment(feedFragmentString);

            BrowserManager.getInstance().setTitle("Easy Insight - " + _goalTree.name);
        }

        private function process(rootNode:GoalTreeNode):void {
            saved = true;
            rootNode.updateName();
            if (rootNode.children != null) {
                for each (var node:GoalTreeNode in rootNode.children) {
                    process(node);
                }
            }
        }

        private function save(saveHandler:Function = null):void {
            process(_goalTree.rootNode);
            var window:KPITreeSaveWindow = new KPITreeSaveWindow();
            if (saveHandler == null) {
                saveHandler = onSave;
            }
            window.addEventListener(KPITreeSaveEvent.SAVE_KPI_TREE, saveHandler, false, 0, true);
            //window.originConnection = connectionID;
            window.kpiTree = _goalTree;
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function onSave(event:KPITreeSaveEvent):void {
            changed = false;
            _goalTree = event.goalTree;
            rootNode = _goalTree.rootNode;
            goalTreeID = _goalTree.goalTreeID;
        }

        private function createPromptWindow(prompt:String, doneHandler:Function):void {
            var window:SavePromptWindow = new SavePromptWindow();
            window.prompt = prompt;
            window.addEventListener(PromptEvent.PROMPT_SAVE, function(event:PromptEvent):void {
                save(function(event:KPITreeSaveEvent):void {
                    doneHandler.call();
                });
            }, false, 0, true);
            window.addEventListener(PromptEvent.PROMPT_DISCARD, function(event:PromptEvent):void {
                doneHandler.call();
            }, false, 0, true);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function close():void {
            if (changed) {
                createPromptWindow("You have unsaved changes to this KPI tree.", function():void {
                    dispatchEvent(new AnalysisCloseEvent());
                });
            } else {
                dispatchEvent(new AnalysisCloseEvent());
            }
        }

        public function cleanup():void {
        }

        public function gotFocus():void {
        }
        ]]>
	</mx:Script>
	<mx:RemoteObject destination="goalService" id="goalService">
		<mx:method name="getGoalTree" result="gotGoalTree()"/>
	</mx:RemoteObject>
	<mx:VBox  width="100%" height="100%" paddingLeft="10" paddingTop="10" paddingRight="10" paddingBottom="10">
		<mx:HBox id="buttonBox" paddingLeft="5" paddingTop="5" paddingBottom="5">
            <mx:Button id="wrapButton" icon="@Embed(source='../../../../assets/document_out.png')" click="close()" toolTip="Close"/>
			<mx:Button id="saveButton" icon="@Embed(source='../../../../assets/floppy_disk.png')" click="save()" toolTip="Save"/>
			<mx:Button id="playButton" icon="@Embed(source='../../../../assets/media_play_green.png')" click="viewData()" toolTip="View Data"
                    enabled="{saved}"/>
		</mx:HBox>
        <goals:GoalTreeAdminWindow goalTreeNode="{rootNode}" goalTreeID="{_goalTreeID}" dataSourceID="{dataSourceID}"/>
	</mx:VBox>
</mx:Module>
