<?xml version="1.0" ?>
<util:EISlimWindow xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:util="com.easyinsight.util.*"
        creationComplete="setupNodes()" width="100%" horizontalAlign="center">
    <mx:Script><![CDATA[
        import com.easyinsight.account.Account;
        import com.easyinsight.framework.User;
        import com.easyinsight.solutions.AddFeedToSolutionWindow;
        import com.easyinsight.solutions.FeedSelectionEvent;
        import com.easyinsight.util.PopUpUtil;

        import mx.collections.ArrayCollection;
        import mx.collections.HierarchicalData;
        import mx.managers.PopUpManager;

        private var uiConfig:UIConfiguration = new UIConfiguration();

        [Bindable]
        private var personaName:String;

        [Bindable]
        private var nodes:ArrayCollection;

        private var _persona:Persona;

        public function set persona(value:Persona):void {
            _persona = value;
        }

        protected override function commitProperties():void {
            super.commitProperties();
            if (_persona != null) {
                personaName = _persona.name;
                uiConfig = UIConfiguration.fromUISettings(_persona.uiSettings);
                dlsList = _persona.dataSourceDLS;
            }
        }

        private function setupNodes():void {
            addEventListener(DLSEvent.DLS_DELETE, removeDataSource);
            var hierarchicalData:HierarchicalData = new HierarchicalData();
            uiVisibility = uiConfig.getRoots();
            hierarchicalData.source = uiVisibility;
            grid.dataProvider = hierarchicalData;
        }

        [Bindable]
        private var uiVisibility:ArrayCollection;

        private function save():void {
            if (_persona == null) {
                _persona = new Persona();
                _persona.dataSourceDLS = dlsList;
                newPersona = true;
            }
            var map:Object = new Object();
            recurseOptions(uiVisibility, map);
            var options:ArrayCollection = new ArrayCollection();
            for each (var setting:UIVisibilitySetting in map) {
                options.addItem(setting);
            }
            _persona.name = personaNameInput.text;
            var settings:UISettings = new UISettings();
            settings.visibilitySettings = options;
            _persona.uiSettings = settings;
            preferencesService.savePersona.send(_persona);
        }

        private function recurseOptions(options:ArrayCollection, configMap:Object):void {
            for each (var option:UIOption in options) {
                if (option.key != null) {
                    var visibilitySetting:UIVisibilitySetting = new UIVisibilitySetting();
                    visibilitySetting.key = option.key;
                    visibilitySetting.selected = option.selected;
                    configMap[option.key] = visibilitySetting;
                }
                recurseOptions(option.children, configMap);
            }
        }

        private var newPersona:Boolean = false;

        private function saved():void {
            var id:int = preferencesService.savePersona.lastResult as int;
            if (User.getInstance().getAccountType() == Account.PERSONAL) {
                User.getInstance().personaID = id;
            }
            _persona.personaID = id;
            if (newPersona) {
                dispatchEvent(new PersonaEvent(PersonaEvent.NEW_PERSONA, _persona));
            } else {

            }
            if (_persona.personaID == User.getInstance().personaID) {
                User.getInstance().changeSettings(_persona.uiSettings);
            }
            PopUpManager.removePopUp(this);
        }

        private function addDataSource():void {
            var window:AddFeedToSolutionWindow = new AddFeedToSolutionWindow();
            window.addEventListener(FeedSelectionEvent.FEED_SELECTED, onDataSource, false, 0, true);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        [Bindable]
        private var dlsList:ArrayCollection = new ArrayCollection();

        private function onDataSource(event:FeedSelectionEvent):void {
            var dls:DataSourceDLS = new DataSourceDLS();
            dls.dataSourceID = event.feedDescriptor.id;
            dls.dataSourceName = event.feedDescriptor.name;
            dls.filters = new ArrayCollection();
            dlsList.addItem(dls);
            var window:DLSConfigWindow = new DLSConfigWindow();
            window.dls = dls;
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function removeDataSource(event:DLSEvent):void {
            dlsList.removeItemAt(dlsList.getItemIndex(event.dls));
        }

        ]]></mx:Script>
    <mx:RemoteObject destination="preferencesService" id="preferencesService">
        <mx:method name="savePersona" result="saved()"/>
    </mx:RemoteObject>
    <mx:VBox paddingLeft="5" paddingBottom="5" paddingRight="5" paddingTop="5">
        <mx:Form>
            <mx:FormItem label="Persona Name:">
                <mx:TextInput id="personaNameInput" text="{personaName}"/>
            </mx:FormItem>
        </mx:Form>
        <mx:TabNavigator id="tabNavigator" width="400" height="300">
            <mx:Box borderThickness="1" borderStyle="solid" label="UI Config">
                <mx:AdvancedDataGrid id="grid" width="100%" height="100%" selectable="false" headerHeight="0">
                    <mx:columns>
                        <mx:AdvancedDataGridColumn headerText="" dataField="label"/>
                        <mx:AdvancedDataGridColumn headerText="" dataField="label" itemRenderer="com.easyinsight.preferences.PersonaConfigCheckbox"
                                sortable="false" width="50"/>
                    </mx:columns>
                </mx:AdvancedDataGrid>
            </mx:Box>
            <mx:VBox borderThickness="1" borderStyle="solid" label="DLS">
                <mx:Button label="Add Data Source..." click="addDataSource()"/>
                <mx:DataGrid id="dataSourceGrid" width="100%" height="100%" selectable="false" headerHeight="0" dataProvider="{dlsList}"
                        rowHeight="28">
                    <mx:columns>
                        <util:EIDataGridColumn headerText="" dataField="display"/>
                        <util:EIDataGridColumn headerText="" itemRenderer="com.easyinsight.preferences.DLSControls" width="100" sortable="false"/>
                    </mx:columns>
                </mx:DataGrid>
            </mx:VBox>
            <!--<mx:VBox borderThickness="1" borderStyle="solid" label="DLS">
                <mx:Button label="Add Data Source..." click="addDataSource()"/>
                <mx:DataGrid id="dataSourceGrid" width="400" height="300" selectable="false" headerHeight="0" dataProvider="{dlsList}">
                    <mx:columns>
                        <util:EIDataGridColumn headerText="" dataField="display"/>
                        <util:EIDataGridColumn headerText="" itemRenderer="com.easyinsight.preferences.DLSControls" width="100" sortable="false"/>
                    </mx:columns>
                </mx:DataGrid>
            </mx:VBox>Â -->
        </mx:TabNavigator>
        <mx:HBox width="100%" horizontalAlign="center">
            <util:SaveButton label="Save" click="save()"/>
            <util:CancelButton label="Cancel" click="PopUpManager.removePopUp(this)"/>
        </mx:HBox>
    </mx:VBox>
</util:EISlimWindow>
