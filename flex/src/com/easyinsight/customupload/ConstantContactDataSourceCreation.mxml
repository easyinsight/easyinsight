<?xml version="1.0" ?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" implements="com.easyinsight.customupload.IServerDataSourceCreation">
    <mx:Metadata>
		[Event(name="userConfigComplete", type="com.easyinsight.customupload.UploadConfigEvent")]
		[Event(name="userConfigCancel", type="com.easyinsight.customupload.UploadConfigEvent")]
	</mx:Metadata>
    <mx:Script><![CDATA[
        import com.easyinsight.administration.feed.FeedDefinitionData;
        import com.easyinsight.datasources.ConstantContactDataSource;

        import com.easyinsight.util.ProgressAlert;

        import mx.controls.Alert;
        import mx.validators.Validator;

        public function get dataSourceDefinition():FeedDefinitionData {
            var source:ConstantContactDataSource = new ConstantContactDataSource();
            source.pin = pinInput.text;
            source.ccUserName = userNameInput.text;
            return source;
        }

        public function set dataSourceDefinition(feedDefinition:FeedDefinitionData):void {
            var ds:ConstantContactDataSource = feedDefinition as ConstantContactDataSource;
            userName = ds.ccUserName;
        }

        [Bindable]
        private var userName:String;

        public function get savesCredentials():Boolean {
            return false;
        }

        public function updateDataSource(feedDefinition:FeedDefinitionData):void {
            var ds:ConstantContactDataSource = feedDefinition as ConstantContactDataSource;
            ds.pin = pinInput.text;
            ds.ccUserName = userNameInput.text;
        }

        private function install():void {
            var results:Array = Validator.validateAll([userNameValidator]);
            if (results.length > 0) {
                userNameInput.setFocus();
                userNameInput.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
            } else {
                ProgressAlert.alert(this, "Retrieving access information...", null, tokenService.getOAuthURL);
                tokenService.getOAuthURL.send(63, 0);
            }
        }

        private function gotURL():void {
            var response:OAuthResponse = tokenService.getOAuthURL.lastResult as OAuthResponse;
            if (response.successful) {
                stackIndex = 1;
                var requestToken:String = response.requestToken;
                navigateToURL(new URLRequest(requestToken));
            } else {
                Alert.show("We encountered a problem with authorizing your account against Constant Contact. Please try again later.");
            }
        }

        [Bindable]
        private var stackIndex:int = 0;

        public function validate():Boolean {
            userNameInput.errorString = null;
            installButton.errorString = null;
            if (pinInput != null) {
                pinInput.errorString = null;
            }
            var results:Array = Validator.validateAll([userNameValidator]);
            if (results.length > 0) {
                userNameInput.setFocus();
                userNameInput.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
                return false;
            }

            if (stackIndex == 0) {
                installButton.errorString = "You need to authorize Easy Insight to access your Constant Contact data.";
                installButton.setFocus();
                installButton.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
                return false;
            }

            if (pinInput.text.length != 6) {
                pinInput.errorString = "You need to enter your six character Verifier from Constant Contact here.";
                pinInput.setFocus();
                pinInput.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
                return false;
            }
            return true;
        }]]></mx:Script>
    <mx:RemoteObject destination="tokenService" id="tokenService">
        <mx:method name="getOAuthURL" result="gotURL()"/>
    </mx:RemoteObject>
    <mx:HBox>
        <mx:Label text="Constant Contact User Name:"/>
        <mx:TextInput id="userNameInput" fontWeight="normal" fontFamily="Lucida Grande" width="250" text="{userName}"/>
    </mx:HBox>
    <mx:TextArea text="Clicking Authorize will open a new browser tab or window to your Constant Contact account. Upon granting access, you will receive a six digit Verifier. Copy that value, return to this tab or window, and paste that value below." borderStyle="none"
            backgroundAlpha="0" editable="false" selectable="false" width="450" height="50"/>

    <mx:Button label="Authorize Easy Insight Access" click="install()" id="installButton"/>
    <mx:ViewStack resizeToContent="true" selectedIndex="{stackIndex}">
        <mx:Box/>
        <mx:Box>
            <mx:Label text="Verifier from Constant Contact:"/>
            <mx:TextInput id="pinInput" fontWeight="normal" fontFamily="Lucida Grande" width="100" maxChars="6"/>
        </mx:Box>
    </mx:ViewStack>
    <mx:StringValidator id="userNameValidator" source="{userNameInput}" property="text" minLength="1"/>
    <mx:StringValidator id="pinValidator" source="{pinInput}" property="text" minLength="6" maxLength="6"/>
</mx:VBox>