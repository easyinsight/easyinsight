<?xml version="1.0" ?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" implements="com.easyinsight.customupload.IServerDataSourceCreation">
    <mx:Metadata>
		[Event(name="userConfigComplete", type="com.easyinsight.customupload.UploadConfigEvent")]
		[Event(name="userConfigCancel", type="com.easyinsight.customupload.UploadConfigEvent")]
	</mx:Metadata>
    <mx:Script><![CDATA[
        import com.easyinsight.administration.feed.FeedDefinitionData;
        import com.easyinsight.datasources.ConstantContactDataSource;

        import com.easyinsight.util.ProgressAlert;

        import mx.controls.Alert;
        import mx.validators.Validator;

        private var def:ConstantContactDataSource;

        public function get dataSourceDefinition():FeedDefinitionData {
            var source:ConstantContactDataSource = new ConstantContactDataSource();
            source.ccUserName = userNameInput.text;
            return source;
        }

        public function set dataSourceDefinition(feedDefinition:FeedDefinitionData):void {
            var ds:ConstantContactDataSource = feedDefinition as ConstantContactDataSource;
            userName = ds.ccUserName;
            this.def = ds;
        }

        [Bindable]
        private var userName:String;

        public function get savesCredentials():Boolean {
            return false;
        }

        public function updateDataSource(feedDefinition:FeedDefinitionData):void {
            var ds:ConstantContactDataSource = feedDefinition as ConstantContactDataSource;
            ds.ccUserName = userNameInput.text;
            ds.briefMode = modeGroup.selectedValue == "brief";
        }

        private function install():void {
            var results:Array = Validator.validateAll([userNameValidator]);
            if (results.length > 0) {
                userNameInput.setFocus();
                userNameInput.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
            } else {
                updateDataSource(def);
                ProgressAlert.alert(this, "Retrieving access information...", null, tokenService.getOAuthURL);
                tokenService.getOAuthURL.send(63, true, null, def, def.visible ? 2 : 1);
            }
        }

        private function gotURL():void {
            var response:OAuthResponse = tokenService.getOAuthURL.lastResult as OAuthResponse;
            if (response.successful) {
                stackIndex = 1;
                var requestToken:String = response.requestToken;
                navigateToURL(new URLRequest(requestToken), "_self");
            } else {
                Alert.show("We encountered a problem with authorizing your account against Constant Contact. Please try again later.");
            }
        }

        [Bindable]
        private var stackIndex:int = 0;

        public function validate():Boolean {
            userNameInput.errorString = null;
            installButton.errorString = null;
            var results:Array = Validator.validateAll([userNameValidator]);
            if (results.length > 0) {
                userNameInput.setFocus();
                userNameInput.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
                return false;
            }

            if (stackIndex == 0) {
                installButton.errorString = "You need to authorize Easy Insight to access your Constant Contact data.";
                installButton.setFocus();
                installButton.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
                return false;
            }
            return true;
        }]]></mx:Script>
    <mx:RemoteObject destination="tokenService" id="tokenService">
        <mx:method name="getOAuthURL" result="gotURL()"/>
    </mx:RemoteObject>
    <mx:TextArea text="Easy Insight can report against two levels of contact info from your Constant Contact data, brief and full. Brief data will only pull over a few fields such as email address and user name, but will also take a fraction of the time required by full to synchronize."
            width="500" height="50" editable="false" borderStyle="none" selectable="false"/>
    <mx:RadioButtonGroup id="modeGroup" selectedValue="brief"/>
    <mx:HBox>
        <mx:RadioButton label="Brief Contact Data" value="brief" selected="true" groupName="modeGroup"/>
        <mx:RadioButton label="Full Contact Data" value="full" groupName="modeGroup"/>
    </mx:HBox>
    <mx:HBox>
        <mx:Label text="Constant Contact User Name:"/>
        <mx:TextInput id="userNameInput" fontWeight="normal" fontFamily="Lucida Grande" width="250" text="{userName}"/>
    </mx:HBox>
    <mx:TextArea text="Clicking Authorize will navigate your browser to your Constant Contact account. Upon granting access, you will be redirected back to Easy Insight to finish the connection process." borderStyle="none"
            backgroundAlpha="0" editable="false" selectable="false" width="450" height="50"/>

    <mx:Button label="Authorize Easy Insight Access" click="install()" id="installButton"/>
    <mx:StringValidator id="userNameValidator" source="{userNameInput}" property="text" minLength="1"/>
</mx:VBox>