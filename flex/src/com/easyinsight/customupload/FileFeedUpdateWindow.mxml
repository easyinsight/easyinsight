<?xml version="1.0" encoding="utf-8"?>
<util:EITitleWindow xmlns:util="com.easyinsight.util.*" xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="doCreationComplete()">
	<mx:Script>
		<![CDATA[
        import com.easyinsight.customupload.wizard.SpreadsheetUpdateWizard;
        import com.easyinsight.util.PopUpUtil;
        import com.easyinsight.util.ProgressAlert;

            import mx.collections.ArrayCollection;

            import mx.managers.PopUpManager;



			private var _feedID:int;
			[Bindable]
            public var file:FileReference = new FileReference();
            
            public function set feedID(feedID:int):void {
            	this._feedID = feedID; 
            }
            
            public function doBrowse( event:Event ):void {
                file.browse();
            }
            
            public function doComplete( event:Event ):void  {
                fileData = file.data;
                /*var xml:XML = new XML(event.data);
				var uploadIDString:String = xml.uploadID.toString();
				var uploadID:Number = parseInt(uploadIDString);
                this.rawUploadID = uploadID;*/
                successMessage.visible = true;
                //formatBox.enabled = true;
                replaceButton.enabled = true;
                appendButton.enabled = true;   
            }

            private var fileData:ByteArray;

            // Called when the window has completed startup
            public function doCreationComplete():void {
                // Listen for when the user has selected a file
                // Listen for when the file upload is complete
                file.addEventListener( Event.SELECT, doSelect );
                file.addEventListener( Event.COMPLETE, doComplete );
            }

            // Called when the user selects a file from disk
            public function doSelect( event:Event ):void {
                // Puts the name of the selected file into the UI
                txtPhoto.text = file.name;
                uploadButton.enabled = true;
            }

            // Called when the "Submit" button is clicked
            // Initiates file upload operation
            public function doSubmit( event:Event ):void {
                progressBar.visible = true;
                file.load();
            }

			
			private function replace():void {
                op = 2;
                ProgressAlert.alert(this, "Analyzing...", null, uploadService.analyzeUpdate);
                uploadService.analyzeUpdate.send(_feedID, fileData);
			}
			
			private function append():void {
                op = 1;
                ProgressAlert.alert(this, "Analyzing...", null, uploadService.analyzeUpdate);
                uploadService.analyzeUpdate.send(_feedID, fileData);
			}
			
			private function cancel():void {
				PopUpManager.removePopUp(this);
			}

            private function updatedData():void {
				PopUpManager.removePopUp(this);
			}
            
            private var op:int = 0;

            private function analyzedUpdate():void {
                var fields:ArrayCollection = uploadService.analyzeUpdate.lastResult as ArrayCollection;
                if (fields.length == 0) {
                    if (op == 1) {
                        ProgressAlert.alert(this, "Updating data...", null, uploadService.updateData);
                        uploadService.updateData.send(_feedID, fileData, false);
                    } else if (op == 2) {
                        ProgressAlert.alert(this, "Updating data...", null, uploadService.updateData);
                        uploadService.updateData.send(_feedID, fileData, true);
                    }
                } else {
                    var window:SpreadsheetUpdateWizard = new SpreadsheetUpdateWizard();
                    window.op = op;
                    window.bytes = fileData;
                    window.fields = fields;
                    window.dataSourceID = _feedID;
                    PopUpManager.addPopUp(window, this, true);
                    PopUpUtil.centerPopUp(window);
                    //dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.SPREADSHEET_UPDATE, {dataSourceID: _feedID, op: op, bytes: fileData, fields: fields})));
                    PopUpManager.removePopUp(this);
                }
            }
		]]>
	</mx:Script>
	<mx:RemoteObject id="uploadService" destination="userUpload">
		<mx:method name="updateData" result="updatedData()"/>
		<mx:method name="analyzeUpdate" result="analyzedUpdate()"/>
	</mx:RemoteObject>
	<mx:VBox id="coreVBox" backgroundColor="#FFFFFF" paddingBottom="10" paddingLeft="10" paddingTop="10" paddingRight="10">
		<mx:Form id="coreForm">
			<mx:FormItem label="Select a file to upload:" direction="horizontal">
				<mx:TextInput id="txtPhoto" editable="false" />
				<mx:Button label="Browse..." click="doBrowse( event )" />
			</mx:FormItem>
			<mx:FormItem label="" direction="horizontal">
				<mx:Button label="Upload" click="doSubmit( event )" id="uploadButton" enabled="false"/>
			</mx:FormItem>
			<mx:FormItem label="" direction="horizontal">
				<mx:HBox horizontalAlign="center">
					<mx:ProgressBar id="progressBar" visible="false" source="{file}"/>
				</mx:HBox>
				<mx:Label text="Upload successful!" visible="false" id="successMessage"/>
			</mx:FormItem>		

		</mx:Form>
        <mx:HBox width="100%" horizontalAlign="center">
            <util:SaveButton label="Replace" click="replace()" enabled="false" id="replaceButton"/>
            <util:SaveButton label="Append" click="append()" enabled="false" id="appendButton"/>
            <util:CancelButton label="Cancel" click="cancel()"/>
        </mx:HBox>
	</mx:VBox>

</util:EITitleWindow>
