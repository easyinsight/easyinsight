<?xml version="1.0"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:util="com.easyinsight.util.*"
        creationComplete="setup()" implements="com.easyinsight.filtering.IFilterDetailEditor">
    <mx:Script><![CDATA[
        import com.easyinsight.analysis.AnalysisItem;
        import com.easyinsight.analysis.AnalysisItemTypes;
        import com.easyinsight.analysis.AnalysisItemWrapper;
        import com.easyinsight.util.PopUpUtil;

        import mx.collections.ArrayCollection;
        import mx.managers.PopUpManager;

        [Bindable]
        private var filterFields:ArrayCollection;

        private function setup():void {
            addEventListener(FieldFilterEvent.REMOVE_FIELD, onRemoveField);
        }

        private function onRemoveField(event:FieldFilterEvent):void {
            filterFields.removeItemAt(filterFields.getItemIndex(event.analysisItem));
        }

        private function addField():void {
            var window:AnalysisItemPickWindow = new AnalysisItemPickWindow();
            window.fields = allFields;
            window.addEventListener(FieldFilterEvent.ADD_FIELD, onAddField, false, 0, true);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function onAddField(event:FieldFilterEvent):void {
            filterFields.addItem(event.analysisItem);
        }

        private var analysisItemFilter:AnalysisItemFilterDefinition;

        public function makeUpdates():FilterDefinition {
            analysisItemFilter.availableItems = filterFields;
            return analysisItemFilter;
        }

        public function set filterDefinition(filterDefinition:FilterDefinition):void {
            analysisItemFilter = filterDefinition as AnalysisItemFilterDefinition;
            filterFields = analysisItemFilter.availableItems;
        }

        public function set feedID(feedID:int):void {
        }

        private var allFields:ArrayCollection;

        public function set fields(value:ArrayCollection):void {
            allFields = new ArrayCollection();
            for each (var wrapper:AnalysisItemWrapper in value) {
                allFields.addItem(wrapper.analysisItem);
            }
        }

        private function bulkAddFields():void {
            var map:Object = new Object();
            for each (var existing:AnalysisItem in analysisItemFilter.availableItems) {
                map[existing.qualifiedName()] = existing;
            }
            for each (var field:AnalysisItem in allFields) {
                if ((field.hasType(AnalysisItemTypes.DIMENSION) || field.hasType(AnalysisItemTypes.DATE)) && map[field.qualifiedName()] == null) {
                    filterFields.addItem(field);
                }
            }
        }
        ]]></mx:Script>
    <mx:HBox>
        <mx:Button label="Add Field..." click="addField()"/>
        <mx:Button label="Bulk Add Fields..." click="bulkAddFields()"/>
    </mx:HBox>
    <mx:DataGrid dataProvider="{filterFields}" dragEnabled="true" dropEnabled="true" dragMoveEnabled="true">
        <mx:columns>
            <util:EIDataGridColumn headerText="Field" dataField="display" width="400"/>
            <util:EIDataGridColumn headerText="" itemRenderer="com.easyinsight.filtering.AnalysisItemFilterControls" width="50"/>
        </mx:columns>
    </mx:DataGrid>
</mx:VBox>
