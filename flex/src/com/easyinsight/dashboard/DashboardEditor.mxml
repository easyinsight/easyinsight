<?xml version="1.0" ?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml"
         xmlns:filtering="com.easyinsight.filtering.*" xmlns:easyinsight="com.easyinsight.*"
         xmlns:analysis="com.easyinsight.analysis.*" xmlns:report="com.easyinsight.report.*"
         implements="com.easyinsight.listing.IPerspective" width="100%" height="100%" creationComplete="populateData()"
         verticalScrollPolicy="off" horizontalScrollPolicy="off">
    <mx:Script><![CDATA[
        import com.easyinsight.analysis.AnalysisCloseEvent;

        import com.easyinsight.analysis.AnalysisItem;
        import com.easyinsight.analysis.AnalysisItemNode;
        import com.easyinsight.analysis.AnalysisItemWrapper;
        import com.easyinsight.analysis.FeedMetadata;
        import com.easyinsight.analysis.PromptEvent;
        import com.easyinsight.analysis.SavePromptWindow;
        import com.easyinsight.analysis.SavedAnalysisEvent;
        import com.easyinsight.code.MarmotScriptEvent;
        import com.easyinsight.code.MarmotScriptWindow;
        import com.easyinsight.commands.CommandEvent;
        import com.easyinsight.commands.CommandProcessor;
        import com.easyinsight.datasources.DataSourceInfo;
        import com.easyinsight.filtering.FilterDefinition;
        import com.easyinsight.filtering.NewFilterWindow;
        import com.easyinsight.framework.PerspectiveInfo;
        import com.easyinsight.framework.User;
        import com.easyinsight.genredata.AnalyzeEvent;
        import com.easyinsight.listing.DescriptorAnalyzeSource;

        import com.easyinsight.solutions.InsightDescriptor;
        import com.easyinsight.util.ActionDashboardLog;
        import com.easyinsight.util.PopUpUtil;

        import com.easyinsight.util.ProgressAlert;
        import com.easyinsight.util.UserAudit;

        import mx.collections.ArrayCollection;
        import mx.collections.Sort;
        import mx.collections.SortField;
        import mx.controls.Alert;
        import mx.core.Application;
        import mx.core.UIComponent;
        import mx.events.CloseEvent;
        import mx.events.FlexEvent;
        import mx.managers.BrowserManager;
        import mx.managers.PopUpManager;
        import mx.utils.URLUtil;

        private var dashboardChanged:Boolean;

        public var embedded:Boolean = false;

        public var urlKey:String;

        private function populateData():void {
            commandProcessor = new CommandProcessor();
            stage.addEventListener(Event.RESIZE, onResize);
            addEventListener(CommandEvent.COMMAND, onCommand);
            addEventListener(DashboardChangedEvent.DASHBOARD_CHANGED, function (event:DashboardChangedEvent):void {
                dashboardChanged = true;
            });
            var controls:ArrayCollection = new ArrayCollection();
            controls.addItem(new DashboardControl("Grid", DashboardGrid));
            controls.addItem(new DashboardControl("Stack", DashboardStack));
            controls.addItem(new DashboardControl("Image", DashboardImage));
            controls.addItem(new DashboardControl("Text", DashboardTextElement));
            this.availableControls = controls;
            if (_dashboardID == 0) {
                dashboardChanged = true;
                setupNewDashboard();
            } else {
                dashboardViewable = true;
                ProgressAlert.alert(this, "Retrieving data...", null, dashboardService.getDashboard);
                dashboardService.getDashboard.send(_dashboardID);
            }
        }

        private function saveAs():void {
            var box:DashboardBox = dashboardCanvas.getChildAt(0) as DashboardBox;
            var results:Array = [];
            box.validate(results);
            if (results.length == 0) {
                box.save();
                _dashboard.rootElement = box.element;
                _dashboard.dataSourceID = dataSourceID;
                _dashboard.filters = transformContainer.getFilterDefinitions();
                var saveDialog:DashboardSaveAsDialog = new DashboardSaveAsDialog();
                saveDialog.dashboard = _dashboard;
                saveDialog.addEventListener(SavedDashboardEvent.SAVED_DASHBOARD, saveAsDashboard);
                PopUpManager.addPopUp(saveDialog, this, true);
                PopUpUtil.centerPopUp(saveDialog);
            } else {
                Alert.show(results[0]);
            }

        }

        private function saveAsDashboard(event:SavedDashboardEvent):void {
            dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.DASHBOARD_EDITOR, {dashboardID:event.dashboard.id})));
        }

        private var _startDataSourceID:int;

        public function set startDataSourceID(value:int):void {
            _startDataSourceID = value;
        }

        private var commandProcessor:CommandProcessor;

        private function onCommand(event:CommandEvent):void {
            commandProcessor.addCommand(event.command);
        }

        private var _dashboardID:int;

        public function set dashboardID(value:int):void {
            _dashboardID = value;
        }

        [Bindable]
        private var _dashboard:Dashboard;

        public function set dashboard(value:Dashboard):void {
            _dashboard = value;
        }

        [Bindable]
        private var availableControls:ArrayCollection;

        [Bindable]
        private var availableReports:ArrayCollection = new ArrayCollection();

        [Bindable]
        private var availableScorecards:ArrayCollection = new ArrayCollection();

        private function gotDashboardMetadata():void {
            var metadata:NewDashboardMetadata = dashboardService.getDashboardEditorMetadata.lastResult as NewDashboardMetadata;
            availableReports = new ArrayCollection();
            var reportList:ArrayCollection = metadata.availableReports;
            for each (var report:InsightDescriptor in reportList) {
                availableReports.addItem(report);
            }
            var sort:Sort = new Sort();
            sort.fields = [ new SortField("name", true) ];
            availableReports.sort = sort;
            availableReports.refresh();
            dataSource = metadata.dataSourceInfo;
        }

        private function gotReports():void {
            availableReports = new ArrayCollection();
            var reportList:ArrayCollection = analysisService.getInsightDescriptorsForDataSource.lastResult as ArrayCollection;
            for each (var report:InsightDescriptor in reportList) {
                availableReports.addItem(report);
            }
            var sort:Sort = new Sort();
            sort.fields = [ new SortField("name", true) ];
            availableReports.sort = sort;
            availableReports.refresh();
        }

        private function gotDataSources():void {
            /*var window:DashboardSetupWindow = new DashboardSetupWindow();
             window.startDataSourceID = _startDataSourceID;
             window.dataSources = feedService.searchForSubscribedFeeds.lastResult as ArrayCollection;
             window.addEventListener(DashboardSetupEvent.DASHBOARD_SETUP, onDimensions, false, 0, true);
             PopUpManager.addPopUp(window, this, true);
             PopUpUtil.centerPopUp(window);*/

        }

        private var metadata:DashboardEditorMetadata = new DashboardEditorMetadata();

        private function gotDashboard():void {
            saveAsEnabled = true;
            _dashboard = dashboardService.getDashboard.lastResult as Dashboard;
            if (!User.getInstance().accountReports && _dashboard.role == 3) {
                saveEnabled = false;
            } else {
                saveEnabled = true;
            }
            if (!_dashboard.absoluteSizing) {
                vbox.percentHeight = 100;
                dashboardCanvas.percentHeight = 100;
            }
            dataSourceID = _dashboard.dataSourceID;
            dataSource = _dashboard.dataSourceInfo;
            metadata.dataSourceID = dataSourceID;
            metadata.editor = true;
            if (!embedded) {
                var feedFragmentObject:Object = new Object();
                feedFragmentObject.dashboardAdminID = String(_dashboard.urlKey);
                var feedFragmentString:String = URLUtil.objectToString(feedFragmentObject);
                BrowserManager.getInstance().setFragment(feedFragmentString);
            }
            var box:DashboardBox = new DashboardBox();
            rootView = box;
            metadata.dashboard = _dashboard;
            box.dashboardEditorMetadata = metadata;
            box.element = _dashboard.rootElement;
            //box.allowDelete = false;
            UIComponent(box).addEventListener(FlexEvent.CREATION_COMPLETE, function (event:FlexEvent):void {
                var filterMap:Object = new Object();
                filterMap["master"] = _dashboard.filters;
                box.updateAdditionalFilters(filterMap);
                box.initialRetrieve();
            });
            dashboardCanvas.addChild(box);
            ProgressAlert.alert(this, "Retrieving reports...", null, analysisService.getInsightDescriptorsForDataSource,
                    dataService.getFeedMetadata);
            analysisService.getInsightDescriptorsForDataSource.send(_dashboard.dataSourceID);
            dataService.getFeedMetadata.send(_dashboard.dataSourceID);
            dashboardFilters = _dashboard.filters;
            UserAudit.instance().log(new ActionDashboardLog(ActionDashboardLog.EDIT, _dashboardID));
        }

        [Bindable]
        private var rootView:IDashboardViewComponent;

        private function setupNewDashboard():void {
            var box:DashboardBox = new DashboardBox();
            box.dashboardEditorMetadata = metadata;
            rootView = box;
            dashboardCanvas.addChild(box);
            _dashboard = new Dashboard();
            _dashboard.dataSourceID = _startDataSourceID;
            metadata.dashboard = _dashboard;
            dataSourceID = _startDataSourceID;
            saveEnabled = true;
            ProgressAlert.alert(this, "Retrieving reports...", null, dashboardService.getDashboardEditorMetadata);
            dashboardService.getDashboardEditorMetadata.send(dataSourceID);
            dataService.getFeedMetadata.send(dataSourceID);
            //_dashboard.rootElement = grid;
        }

        private function closeEditor():void {
            if (dashboardChanged) {
                createPromptWindow("You have unsaved changes. Are you sure you want to exit?", function ():void {
                    dispatchEvent(new AnalysisCloseEvent(feedMetadata.urlKey));
                });
            } else {
                dispatchEvent(new AnalysisCloseEvent(feedMetadata.urlKey));
            }
        }

        override protected function createChildren():void {
            super.createChildren();
            if (embedded) {
                logonStatus.parent.removeChild(logonStatus);
            }
        }

        private function alertListener(event:CloseEvent):void {
            if (event.detail == Alert.OK) {
                dispatchEvent(new AnalysisCloseEvent());
            }
        }

        private function save(saveHandler:Function = null):void {
            var box:DashboardBox = dashboardCanvas.getChildAt(0) as DashboardBox;
            var results:Array = [];
            box.validate(results);
            if (results.length == 0) {
                box.save();
                _dashboard.rootElement = box.element;
                _dashboard.dataSourceID = dataSourceID;
                _dashboard.filters = transformContainer.getFilterDefinitions();
                var window:DashboardSaveWindow = new DashboardSaveWindow();
                if (saveHandler == null) {
                    saveHandler = onSave;
                }
                window.addEventListener(DashboardSaveEvent.DASHBOARD_SAVE, saveHandler, false, 0, true);
                window.exchangeSave = exchangeSave;
                window.dashboard = _dashboard;
                PopUpManager.addPopUp(window, this, true);
                PopUpUtil.centerPopUp(window);
            } else {
                Alert.show(results[0]);
            }
        }

        private function onSave(event:DashboardSaveEvent):void {
            dashboardChanged = false;
            saveAsEnabled = true;
            dashboardViewable = true;
            this._dashboard = event.dashboard;
        }

        [Bindable]
        private var dataSourceID:int;

        [Bindable]
        private var availableDimensions:ArrayCollection;

        [Bindable]
        private var index:int;

        private function viewDashboard():void {
            if (dashboardChanged) {
                Alert.show("You have unsaved changes. Are you sure you want to exit?", "Alert",
                        Alert.OK | Alert.CANCEL, null, viewListener, null, Alert.CANCEL);
            } else {
                dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.DASHBOARD_VIEW, {dashboardID:_dashboard.id})));
            }
        }

        private function viewListener(event:CloseEvent):void {
            if (event.detail == Alert.OK) {
                dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.DASHBOARD_VIEW, {dashboardID:_dashboard.id})));
            }
        }

        private function configureDashboard():void {
            var window:DashboardWindow = new DashboardWindow();
            window.dashboard = _dashboard;
            window.reports = availableReports;
            window.addEventListener(Event.CHANGE, onDashboardChange, false, 0, true);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function onDashboardChange(event:Event):void {
        }

        [Bindable]
        private var allFields:ArrayCollection;

        private var feedMetadata:FeedMetadata;

        private var dashboardFilters:ArrayCollection = new ArrayCollection();

        private var exchangeSave:Boolean;

        private function gotFeedMetadata():void {
            var feedMetadata:FeedMetadata = dataService.getFeedMetadata.lastResult as FeedMetadata;
            this.feedMetadata = feedMetadata;
            if (_dashboard == null || _dashboard.id == 0) {
                if (!embedded) {
                    var feedFragmentObject:Object = new Object();
                    feedFragmentObject.newDashboardID = feedMetadata.urlKey;
                    var feedFragmentString:String = URLUtil.objectToString(feedFragmentObject);
                    BrowserManager.getInstance().setFragment(feedFragmentString);
                }
            }
            exchangeSave = feedMetadata.exchangeSave;
            allFields = new ArrayCollection();
            var dimensions:ArrayCollection = new ArrayCollection();
            for each (var analysisItem:AnalysisItem in feedMetadata.fields) {
                dimensions.addItem(analysisItem);
                var aNode:AnalysisItemNode = new AnalysisItemNode();
                aNode.analysisItem = analysisItem;
                allFields.addItem(new AnalysisItemWrapper(aNode));
            }
            this.availableDimensions = dimensions;
            if (transformContainer.getFilterDefinitions().length == 0) {
                for each (var filter:FilterDefinition in dashboardFilters) {
                    transformContainer.addFilterDefinition(filter);
                }
            }
            metadata.allFields = allFields;
            metadata.dataSourceID = dataSourceID;
            metadata.availableFields = availableDimensions;
            metadata.transformContainer = transformContainer;
        }

        [Bindable]
        private var dashboardViewable:Boolean = false;

        private function editCode():void {
            var window:MarmotScriptWindow = new MarmotScriptWindow();
            window.script = _dashboard.marmotScript;
            window.addEventListener(MarmotScriptEvent.SAVE_SCRIPT, onScript, false, 0, true);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function transformsUpdated(event:Event):void {
            var filterMap:Object = new Object();
            if (transformContainer != null) {
                filterMap["master"] = transformContainer.getFilterDefinitions();
            }
            if (rootView != null) {
                rootView.updateAdditionalFilters(filterMap);
                rootView.refresh();
            }
        }

        private function onScript(event:MarmotScriptEvent):void {
            _dashboard.marmotScript = event.script;
        }

        public function gotFocus():void {
        }

        public function cleanup():void {
            stage.removeEventListener(Event.RESIZE, onResize);
        }

        private function newReport():void {
            if (dashboardChanged) {
                createPromptWindow("You have unsaved changes to this dashboard.", function ():void {
                    dispatchEvent(new AnalyzeEvent(new DescriptorAnalyzeSource(dataSourceID, feedMetadata.urlKey)));
                });
            } else {
                dispatchEvent(new AnalyzeEvent(new DescriptorAnalyzeSource(dataSourceID, feedMetadata.urlKey)));
            }
        }

        private function newDashboard():void {
            if (dashboardChanged) {
                createPromptWindow("You have unsaved changes to this dashboard.", function ():void {
                    dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.DASHBOARD_EDITOR, {startDataSourceID:dataSourceID, urlKey:feedMetadata.urlKey})));
                });
            } else {
                dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.DASHBOARD_EDITOR, {startDataSourceID:dataSourceID, urlKey:feedMetadata.urlKey})));
            }
        }

        private function createRequiredPromptWindow(prompt:String, doneHandler:Function):void {
            var window:SavePromptWindow = new SavePromptWindow();
            window.prompt = prompt;
            window.defineOption("Save", PromptEvent.PROMPT_SAVE);
            window.defineOption("Cancel", PromptEvent.PROMPT_CANCEL);
            window.addEventListener(PromptEvent.PROMPT_SAVE, function (event:PromptEvent):void {
                save(function (event:SavedAnalysisEvent):void {
                    doneHandler.call();
                });
            }, false, 0, true);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function createPromptWindow(prompt:String, doneHandler:Function):void {
            var window:SavePromptWindow = new SavePromptWindow();
            window.prompt = prompt;
            window.addEventListener(PromptEvent.PROMPT_SAVE, function (event:PromptEvent):void {
                save(function (event:SavedAnalysisEvent):void {
                    doneHandler.call();
                });
            }, false, 0, true);
            window.addEventListener(PromptEvent.PROMPT_DISCARD, function (event:PromptEvent):void {
                doneHandler.call();
            }, false, 0, true);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function goReport():void {
            if (_dashboard.id == 0) {
                createRequiredPromptWindow("You need to save this dashboard first.", function ():void {
                    dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.DASHBOARD_VIEW, {dashboardID:_dashboard.id})));
                });
            } else if (dashboardChanged) {
                createRequiredPromptWindow("You have unsaved changes to this dashboard.", function ():void {
                    dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.DASHBOARD_VIEW, {dashboardID:_dashboard.id})));
                });
            } else {
                dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.DASHBOARD_VIEW, {dashboardID:_dashboard.id})));
            }
        }

        private function goAdmin():void {
            if (dashboardChanged) {
                createPromptWindow("You have unsaved changes to this dashboard.", function ():void {
                    dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.DATA_SOURCE_ADMIN, {feedID:dataSourceID, urlKey:feedMetadata.urlKey})));
                });
            } else {
                dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.DATA_SOURCE_ADMIN, {feedID:dataSourceID, urlKey:feedMetadata.urlKey})));
            }
        }

        override protected function measure():void {
            super.measure();
            if (controls.parent) {
                vbox.width = Application(Application.application).width - controls.width - 30;
            } else {
                vbox.width = Application(Application.application).width - 30;
            }
            vbox.maxHeight = Application(Application.application).height - headers.height - filtersBox.height - 20;
            lastFiltersHeight = filtersBox.height;
        }

        private var lastFiltersHeight:int;

        override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void {
            super.updateDisplayList(unscaledWidth, unscaledHeight);
            if (filtersBox.height != lastFiltersHeight) {
                lastFiltersHeight = filtersBox.height;
                vbox.maxHeight = Application(Application.application).height - headers.height - filtersBox.height - 20;
            }
            //vbox.width = this.width - controls.width;
            //vbox.maxHeight = this.height - headers.height - filtersBox.height;

            //Alert.show("width set to " + (this.width - controls.width));
            //Alert.show("max height set to " + (this.height - headers.height - filtersBox.height) + " where this height = " + this.height);
        }

        private function onResize(event:Event):void {
            invalidateSize();
        }

        // >=
        //
        private function viewInHTML():void {
            if (_dashboard.id == 0) {
                createRequiredPromptWindow("You have unsaved changes to this dashboard.", function ():void {
                    navigateToURL(new URLRequest("/app/html/dashboard/" + _dashboard.urlKey), "_blank");
                });
            } else if (dashboardChanged) {
                createPromptWindow("You have unsaved changes to this report.", function ():void {
                    navigateToURL(new URLRequest("/app/html/dashboard/" + _dashboard.urlKey), "_blank");
                });
            } else {
                navigateToURL(new URLRequest("/app/html/dashboard/" + _dashboard.urlKey), "_blank");
            }
        }

        private function refreshReports():void {
            analysisService.getInsightDescriptorsForDataSource.send(dataSourceID);
        }

        private function toggleEditorControls():void {
            if (controls.parent) {
                toggleButton.label = "Show Editor Controls";
                controls.parent.removeChild(controls);
                DashboardBox(rootView).toggleControls(false);
            } else {
                toggleButton.label = "Hide Editor Controls";
                coreHBox.addChildAt(controls, 0);
                DashboardBox(rootView).toggleControls(true);
            }
            invalidateSize();
        }

        [Bindable]
        [Embed(source="../../../../assets/refresh.png")]
        private var refreshIcon:Class;

        private var _dataSource:DataSourceInfo;


        [Bindable(event="dataSourceChanged")]
        public function get dataSource():DataSourceInfo {
            return _dataSource;
        }

        public function set dataSource(value:DataSourceInfo):void {
            if (_dataSource == value) return;
            _dataSource = value;
            dispatchEvent(new Event("dataSourceChanged"));
        }

        [Bindable]
        private var saveAsEnabled:Boolean = false;

        [Bindable]
        private var saveEnabled:Boolean = false;
        ]]></mx:Script>
    <mx:RemoteObject destination="analysisDefinition" id="analysisService">
        <mx:method name="getInsightDescriptorsForDataSource" result="gotReports()"/>
    </mx:RemoteObject>
    <mx:RemoteObject destination="dashboardService" id="dashboardService">
        <mx:method name="getDashboard" result="gotDashboard()"/>
        <mx:method name="getDashboardEditorMetadata" result="gotDashboardMetadata()"/>
    </mx:RemoteObject>
    <mx:RemoteObject destination="data" id="dataService">
        <mx:method name="getFeedMetadata" result="gotFeedMetadata()"/>
    </mx:RemoteObject>
    <mx:RemoteObject id="feedService" destination="feeds">
        <mx:method name="searchForSubscribedFeeds" result="gotDataSources()"/>
    </mx:RemoteObject>
    <mx:HBox width="100%" styleName="topButtonBar" id="headers">
        <mx:Button id="wrapButton" icon="@Embed(source='../../../../assets/document_out.png')" click="closeEditor()"
                   toolTip="Close" labelPlacement="right" label="Close Editor" styleName="grayButton"/>
        <mx:Button id="saveButton" icon="@Embed(source='../../../../assets/floppy_disk.png')" click="save()"
                   toolTip="Save" labelPlacement="right" label="Save" styleName="grayButton" enabled="{saveEnabled}"/>
        <mx:Button id="saveAsButton" icon="@Embed(source='../../../../assets/save_as.png')" click="saveAs()"
                   toolTip="Save As" enabled="{saveAsEnabled}" label="Save As"
                   labelPlacement="right" styleName="grayButton"/>
        <report:RefreshButton styleName="grayButton" labelPlacement="right"
                              id="refreshButton" toolTip="Refresh this dashboard" label="Refresh" viewFactory="{rootView}"
                              dataSource="{dataSource}" icon="{refreshIcon}"/>
        <mx:Button icon="@Embed(source='../../../../assets/document_edit.png')" click="configureDashboard()"
                   toolTip="Configure Dashboard" label="Configure Dashboard" labelPlacement="right"
                   styleName="grayButton"/>
        <mx:Button icon="@Embed(source='../../../../assets/code_edit.png')" click="editCode()"
                   toolTip="Edit Code" label="Edit Code" labelPlacement="right" styleName="grayButton"/>
        <analysis:AdditionalActionButton newReportAction="newReport()" newDashboardAction="newDashboard()"
                                         reportViewAction="goReport()"
                                         viewInHTMLAction="viewInHTML()" configureDataSourceAction="goAdmin()"
                                         toolTip="Actions"
                                         icon="@Embed(source='../../../../assets/businessman_edit.png')"
                />
        <mx:Button styleName="grayButton" id="toggleButton" label="Hide Editor Controls" click="toggleEditorControls()"/>
        <mx:Spacer width="100%"/>
        <easyinsight:LoggedOnStatus textColor="#FFFFFF" id="logonStatus"/>
    </mx:HBox>
    <mx:HBox width="100%" height="100%" paddingLeft="10" paddingRight="10" verticalScrollPolicy="off"
             horizontalScrollPolicy="off" id="coreHBox">
        <mx:VBox height="100%" horizontalAlign="center" width="200" paddingTop="20" paddingBottom="20" id="controls">
            <mx:Label text="Controls" fontSize="14"/>
            <mx:List id="controlsList" dataProvider="{availableControls}" labelField="name" dragEnabled="true"
                     width="100%" rowCount="4"/>
            <mx:HBox verticalAlign="middle">
                <mx:Label text="Reports" fontSize="14"/>
                <mx:LinkButton label="Refresh" click="refreshReports()" fontSize="10"/>
            </mx:HBox>
            <mx:List id="reportsList" dataProvider="{availableReports}" labelField="name" dragEnabled="true"
                     width="100%" height="100%"/>
        </mx:VBox>
        <mx:VBox width="100%" height="100%" horizontalAlign="center" verticalScrollPolicy="off"
                 horizontalScrollPolicy="off">
            <mx:HBox width="100%" id="filtersBox">
                <mx:Button icon="@Embed(source='../../../../assets/funnel.png')"
                           toolTip="Create New Filter..." click="transformContainer.addNewFilter()" label="New Filter"/>
                <filtering:TransformContainer id="transformContainer" width="100%" height="100%"
                                              borderStyle="solid" dashboard="{_dashboard}"
                                              borderThickness="1" backgroundColor="#ffffff"
                                              backgroundAlpha=".1" feedID="{dataSourceID}"
                                              analysisItems="{allFields}" filterSource="{NewFilterWindow.DASHBOARD_LEVEL}"
                                              updatedTransforms="transformsUpdated(event)" context="{TransformContainer.DASHBOARD_EDITOR}"/>
            </mx:HBox>
            <mx:Canvas width="100%" height="100%" verticalScrollPolicy="off" horizontalScrollPolicy="off">
                <mx:VBox id="vbox" verticalScrollPolicy="on" verticalGap="0">
                    <mx:Canvas width="100%" id="dashboardCanvas"/>
                </mx:VBox>
            </mx:Canvas>
        </mx:VBox>
    </mx:HBox>
</mx:VBox>