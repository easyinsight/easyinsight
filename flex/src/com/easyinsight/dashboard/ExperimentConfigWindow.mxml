<?xml version="1.0"?>
<util:EISlimWindow xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:util="com.easyinsight.util.*" creationComplete="onCreation()">
    <mx:Script><![CDATA[
        import com.easyinsight.listing.Tag;
        import com.easyinsight.solutions.InsightDescriptor;
        import com.easyinsight.util.ProgressAlert;

        import mx.collections.ArrayCollection;
        import mx.managers.PopUpManager;

        [Bindable]
        private var reports:ArrayCollection;

        public var dashboardReportViewComponent:DashboardReportViewComponent;

        private function onCreation():void {
            reportID = dashboardReportViewComponent.dashboardReport.report.id;
            ProgressAlert.alert(this, "Retrieving reports...", null, analysisService.getInsightDescriptorsForDataSource);
            analysisService.getInsightDescriptorsForDataSource.send(dashboardReportViewComponent.dashboardEditorMetadata.dataSourceID);
        }

        private function useThisReport():void {
            dashboardReportViewComponent.onReportConfigure(InsightDescriptor(reportBox.selectedItem));
            PopUpManager.removePopUp(this);
        }

        [Bindable]
        private var reportID:int;

        private function gotReports():void {
            var reports:ArrayCollection = analysisService.getInsightDescriptorsForDataSource.lastResult as ArrayCollection;
            var tagID:int = dashboardReportViewComponent.dashboardReport.recommendedTag;
            if (tagID > 0) {
                var matchingReports:ArrayCollection = new ArrayCollection();
                for each (var rep:InsightDescriptor in reports) {
                    for each (var tag:Tag in rep.tags) {
                        if (tag.id == tagID) {
                            matchingReports.addItem(rep);
                            break;
                        }
                    }
                }
                reports = matchingReports;
            }
            this.reports = reports;
        }
        ]]></mx:Script>
    <mx:RemoteObject id="analysisService" destination="analysisDefinition">
        <mx:method name="getInsightDescriptorsForDataSource" result="gotReports()"/>
    </mx:RemoteObject>
    <util:SmartComboBox dataProvider="{reports}" id="reportBox" selectedProperty="id" selectedValue="{reportID}" labelField="name"/>
    <mx:HBox>
        <util:SaveButton label="Use This Report" click="useThisReport()"/>
        <util:CancelButton label="Cancel" click="PopUpManager.removePopUp(this)"/>
    </mx:HBox>
</util:EISlimWindow>
